
Require Export basic_geometric_facts.
(* basic_geometric_facts:
Require Export geometry_tools.

Theorem field_prop1 : forall a : F, -a=0 -> a=0.

Hint Resolve field_prop1 : Geom.

Theorem col_1 : forall A B C : Point, Col A B C -> Col B A C.

Theorem col_2 : forall A B C : Point, Col A B C -> Col A C B.

Theorem col_3 : forall A B C : Point, Col A B C -> Col B C A.

Theorem col_4 : forall A B C : Point, Col A B C -> Col C B A.

Theorem col_5 : forall A B C : Point, Col A B C -> Col C A B.

Hint Resolve col_1 col_2 col_3 col_4 col_5: Geom.

Theorem noteqnotzero : forall A B : Point, A <> B -> A ** B <> 0.
Hint Resolve noteqnotzero: Geom.

Theorem egalcol : forall A B C : Point, A = B -> Col A B C.

Theorem notcolnotegal_1 : forall A B C : Point, ~ Col A B C -> A <> B.

Theorem notcolnotegal_2 : forall A B C : Point, ~ Col A B C -> B <> C.

Theorem notcolnotegal_3 : forall A B C : Point, ~ Col A B C -> A <> C.

Hint Resolve notcolnotegal_1 notcolnotegal_2 notcolnotegal_3: Geom.

Theorem notparallelnotegal_1 : forall A B C D, 
 ~ parallel A B C D -> A<>B.

Theorem notparallelnotegal_2 : forall A B C D, 
 ~ parallel A B C D -> C<>D.

Hint Resolve notparallelnotegal_1 notparallelnotegal_2 : Geom.

Theorem dirseg_1 :
 forall A B C D : Point, D ** C <> 0 -> A ** B / C ** D = B ** A / D ** C.
Hint Resolve dirseg_1: Geom.

Theorem dirseg_2 : forall A B C D : Point, A ** B = C ** D -> B ** A = D ** C.
Hint Resolve dirseg_2: Geom.

Theorem dirseg_3 :
 forall A B C D : Point, C ** D <> 0 -> A ** B / C ** D = - (B ** A / C ** D).

Theorem dirseg_4 :
 forall A B C D : Point, C ** D <> 0 -> A ** B / C ** D = - (A ** B / D ** C).

Hint Resolve dirseg_3 dirseg_4: Geom.

Theorem dirseg_simpl_1 : 
 forall A B, A<>B -> A**B / A**B = 1.

Theorem dirseg_simpl_2 : 
 forall A B, A<>B -> B**A / A**B = -(1).

Theorem dirsur_1 :
 forall A B C D E F : Point,
 ~ Col D E F -> S A B C / S D E F = S A C B / S D F E.
Hint Resolve dirsur_1: Geom.

Theorem col_trans_1 :
 forall A B C D : Point, A <> B -> Col A B C -> Col A B D -> Col A C D.

Hint Resolve col_trans_1.

Lemma build_point_not_collinear_1 : forall A B, A<>B -> exists C, ~ Col A B C. 

Lemma build_point_not_collinear_2 : forall A, exists B, exists C, ~ Col A B C. 

Theorem col_not_col_1 :
 forall A B O P : Point, A <> O -> Col A P O -> ~ Col P A B -> ~ Col O A B.

Hint Resolve col_not_col_1.

Theorem A6_1 :
 forall P Q A M : Point,
 M <> Q -> ~ Col Q A M -> Col P Q M -> P ** M / Q ** M = S P A M / S Q A M.
Hint Resolve A6_1: Geom.

Theorem A6_2 :
 forall P Q R A : Point,
 P <> Q -> ~ Col Q A P -> Col P Q R -> P ** R / P ** Q = S R A P / S Q A P.
Hint Resolve A6_2: Geom.

Theorem A6_3 :
 forall A B C P : Point,
 A <> C -> ~ Col P A C -> Col A B C -> C ** B / A ** C = S P C B / S P A C.
Hint Resolve A6_3: Geom.

Theorem A6_4 :
 forall A B C P : Point,
 A <> B -> ~ Col P A B -> Col A B C -> C ** A / A ** B = S P C A / S P A B.
Hint Resolve A6_4: Geom.

Theorem A6_5 :
 forall A B O P : Point,
 O <> A -> ~ Col O A B -> Col P A O -> P ** A / O ** A = S P A B / S O A B.

Hint Resolve A6_5: Geom.

Theorem A6_6 :
 forall A B R Q : Point,
 A <> Q -> ~ Col Q A B -> Col A R Q -> A ** R / A ** Q = S R A B / S Q A B.

Hint Resolve A6_6: Geom.

Theorem l2_7 :
 forall A B C D P : Point,
 Col C A B ->
 Col D A B ->
 ~ Col P C A -> ~ Col P A B -> A <> B -> S P C D / S P A B = C ** D / A ** B.

Theorem par_aux_1 : forall P Q U V : Point, ~ parallel P Q U V -> P <> Q.

Theorem par_not_all_col :
 forall P Q U V : Point, ~ parallel P Q U V -> ~ Col P U Q \/ ~ Col P Q V. 

Theorem co_side_main :
 forall A B P Q M : Point,
 ~ Col Q A B ->
 ~ Col P A M ->
 ~ Col Q A M ->
 Q <> M -> Col A B M -> Col P Q M -> P ** M / Q ** M = S P A B / S Q A B.

Theorem co_side :
 forall A B P Q M : Point,
 ~ Col Q A B ->
 Q <> M -> Col A B M -> Col P Q M -> P ** M / Q ** M = S P A B / S Q A B.

Hint Resolve co_side: Geom.

Theorem inter_unicity :
 forall A B P Q M : Point,
 ~ parallel P Q A B -> Col A B M -> Col P Q M -> A = Q -> A = M.

Theorem inter_unicity_2 :
 forall A B P Q M N: Point,
 ~ parallel P Q A B -> 
 Col A B M -> Col P Q M -> 
 Col A B N -> Col P Q N -> 
 M = N.

Theorem co_side_bis :
 forall A B P Q M : Point,
 ~ parallel P Q A B ->
 Col A B M -> Col P Q M -> Q ** M / P ** Q = S Q A B / S4 P A Q B.

Hint Resolve co_side_bis: Geom.

Theorem co_side_ter :
 forall A B P Q M : Point,
 ~ parallel P Q A B ->
 Col A B M -> Col P Q M -> P ** M / P ** Q = S P A B / S4 P A Q B.

Hint Resolve co_side_ter: Geom.

Theorem l2_9_weak :
 forall A B P Q R : Point,
 P <> Q ->
 ~ Col Q A P ->
 ~ Col Q P B ->
 Col R P Q -> S R A B = P ** R / P ** Q * S Q A B + R ** Q / P ** Q * S P A B.

Theorem l2_9aux :
 forall A B P Q R : Point,
 P <> Q ->
 Col R P Q ->
 Col Q A P ->
 ~ Col Q P B -> S R A B = P ** R / P ** Q * S Q A B + R ** Q / P ** Q * S P A B.

Theorem l2_9 :
 forall A B P Q R : Point,
 P <> Q ->
 Col R P Q -> S R A B = P ** R / P ** Q * S Q A B + R ** Q / P ** Q * S P A B.

Theorem A2bgen :
 forall (A B P P' : Point) (r : F),
 Col A B P -> A ** P = r * A ** B -> Col A B P' -> A ** P' = r * A ** B -> P = P'.

Definition mid_point (I A B : Point) : Prop := 
  Col A B I /\ A ** I = I ** B.

Definition symmetric_point (I A B : Point) : Prop :=
  Col A B I /\ A ** B = B ** I.

Theorem mid_point_ex : forall A B : Point, {O : Point | mid_point O A B}.

Theorem symmetric_point_ex :
 forall A O : Point, {A' : Point | Col A O A' /\ A ** O = O ** A'}.

Lemma mid_point_comm :
  forall A B C,
  mid_point A B C -> mid_point A C B.

Hint Immediate mid_point_comm : Geom.

Theorem diag_mid_point_parallel : forall O A P B Q,
mid_point O A P -> mid_point O B Q -> parallel A B P Q.

Theorem euclid_parallel_existence_strong :
 forall A B P : Point, A<>B -> {Q : Point | parallel A B P Q /\ P<>Q}.

Theorem euclid_parallel_existence :
 forall A B P : Point, {Q : Point | parallel A B P Q}.

Theorem par_1 : forall A B C D : Point, 
   parallel A B C D -> parallel B A C D.

Hint Resolve par_1: Geom.

Theorem par_2 : forall A B C D : Point, 
   parallel A B C D -> parallel C D A B.

Hint Resolve par_2: Geom.

Theorem not_parallel_not_eq_1 : forall A B C D,
  ~ parallel A B C D -> A<>B. 

Theorem not_parallel_not_eq_2 : forall A B C D,
  ~ parallel A B C D -> C<>D. 

Hint Resolve not_parallel_not_eq_1 not_parallel_not_eq_2 : Geom.

Lemma mid_point_degenerated_1 : forall A B, mid_point A A B -> A=B.

Lemma mid_point_degenerated_2 : forall A B, mid_point A B B -> A=B.

Lemma mid_point_degenerated_3 : forall A B, mid_point A B A -> A=B.

Hint Resolve mid_point_degenerated_1 mid_point_degenerated_2 mid_point_degenerated_3:Geom.

Lemma eq_diff_diff : forall A B C D, A<>B -> A**B=C**D -> C<>D.

Hint Resolve eq_diff_diff : Geom.

Lemma parallel_side_eq_parallel' : forall P Q C D,
  parallel P Q C D -> P**Q=C**D -> parallel D Q P C. *)

Theorem common_point_not_par_aux :
 forall A B C D Y : Point,
 Col Y A B ->
 Col Y C D -> 
 A <> B -> 
 C <> D -> 
 ~ Col D A B -> 
 ~ Col Y A C -> 
 ~ parallel A B C D.
Proof with Geometry. 
intros.
assert (D <> Y).
unfold not;intro;subst D...
unfold not;intro.
assert (C ** Y / D ** Y = S C A B / S D A B)...
assert (S C A B = S D A B).
unfold parallel in H6.
unfold S4 in H6.
assert (S C A B = - S A C B)...
rewrite H8.
RewriteVar (S A C B) H6.
ring_simplify.
Geometry.
rewrite H8 in H7.
assert (C ** Y = D ** Y).
RewriteVar (C ** Y) H7.
field...
Geometry.
assert (C = D).
assert (C ** D + D ** Y = C ** Y)...
rewrite <- H10 in H9.
assert (C ** D = 0)...
RewriteVar (C ** D) H9...
Geometry.
Qed.

Lemma col_par_1 : forall A B C,
 Col A B C -> parallel A B B C.
Proof.
intros.
unfold parallel, S4, Col in *.
rewrite H.
basic_simpl.
ring.
Qed.

Lemma col_par_2 : forall A B C,
 Col A B C -> parallel A B C B.
Proof.
intros.
unfold parallel, S4, Col in *.
basic_simpl.
uniformize_signed_areas.
rewrite H.
ring.
Qed.

Lemma col_par_3 : forall A B C,
 Col A B C -> parallel B A C B.
Proof.
intros.
unfold parallel, S4, Col in *.
basic_simpl.
uniformize_signed_areas.
rewrite H.
ring.
Qed.

Lemma col_par_4 : forall A B C,
 Col A B C -> parallel B A B C.
Proof.
intros.
unfold parallel, S4, Col in *.
basic_simpl.
uniformize_signed_areas.
rewrite H.
ring.
Qed.

Hint Resolve col_par_1 col_par_2 col_par_3 col_par_4 : Geom. 

Lemma par_col_col_1 : forall A B C D, 
 parallel A B C D ->
 Col A B C -> 
 Col A B D.
Proof.
intros.
unfold parallel,S4,Col in *.
uniformize_signed_areas.
RewriteVar (S A B D) H.
replace (- (1) * S A C B) with (- S A C B) by ring.
auto.
Qed.

Lemma par_col_col_2 : forall A B C D, 
 parallel A B C D ->
 Col A B D -> 
 Col A B C.
Proof.
intros.
assert (parallel A B D C).
Geometry.
eapply par_col_col_1;eauto.
Qed.

Lemma par_col_col_3 : forall A B C D, 
 parallel A B C D ->
 Col A C D -> 
 Col B C D.
Proof with Geometry.
intros.
cut (Col C D B).
Geometry.
assert (parallel C D A B).
Geometry.
eapply par_col_col_1;eauto...
Qed.

Lemma par_col_col_4 : forall A B C D, 
 parallel A B C D ->
 Col B C D -> 
 Col A C D.
Proof.
intros.
cut (Col C D A).
Geometry.
assert (parallel C D B A).
Geometry.
eapply par_col_col_1;eauto.
Geometry.
Qed.

Theorem common_point_not_par :
 forall A B C D Y : Point,
 Col Y A B -> 
 Col Y C D -> 
 A <> B -> 
 C <> D ->
 ~ Col Y A C -> 
 ~ parallel A B C D. 
Proof with Geometry.
intros.
cases_col D A B.
unfold parallel, S4 in |- *.
cut (Y = D).
intro.
assert (S A B D = S D A B)...
rewrite H6.
rewrite H4.
unfold not in |- *.
intro.
NormalizeRing H7.
clear H H0.
rewrite H5 in H3.
clear H5.
clear H6.
assert (Col A B D)...
assert (Col A B C)...
assert (Col A D C); eauto with Geom.
assert (Col A B Y)...
assert (Col A B D)...
assert (Col A Y D); eauto with Geom.
assert (A <> Y); eauto with Geom.
cases_equality Y D.
auto.
assert (Col Y D A)...
assert (Col Y D C)...
assert (Col Y A C); eauto with Geom.
intuition.
eapply common_point_not_par_aux; apply H || auto.
Qed.

Definition parallelogram (A B C D : Point) : Prop :=
  parallel A B C D /\ parallel B C A D /\ ~ Col A B C.

Theorem l1_24 :
 forall A B C D O : Point,
 parallelogram A B C D -> 
 Col A C O -> 
 Col B D O -> 
 A ** O = O ** C.
Proof with Geometry.
intros.
unfold parallelogram in H.
DecompAndAll.
unfold parallel,S4 in *.
assert (S A B D = S A B C).
RewriteVar (S A B D) H2...
replace (-(1) * S A C B) with (- S A C B) by ring...
assert (S B C D = S B C A).
RewriteVar (S B C D) H4...
replace (-(1) * S B A C) with (- S B A C) by ring...
assert (S C B D = - S B C A).
rewrite <- H3...
assert (~ Col C B D).
unfold not in |- *; intro.
rewrite H7 in H6...
assert (A ** O / C ** O = S A B D / S C B D).
apply co_side...
unfold not in |- *; intro.
subst O...
rewrite H in H8...
rewrite H6 in H8...
assert (S A B C = S B C A)...
rewrite H9 in H8...
assert (C ** O = - O ** C)...
rewrite H10 in H8...
RewriteVar (A ** O) H8...
field...
rewrite <- H10.
assert (O <> C).
unfold not; intro;subst O...
Geometry.
Qed.

Theorem diago_par_intersect :
 forall A B C D : Point, 
 parallelogram A B C D -> 
 ~ parallel A C B D.
Proof with Geometry.
unfold parallelogram,parallel, S4 in |- *.
intros.
DecompAndAll.
assert (S A C D = S A C B + S A B D + S B C D)...
RewriteVar (S A C B) H0.
RewriteVar (S B C D) H2.
NormalizeRing H.
rewrite H.
replace (S B A C) with (- S A B C)...
ring_simplify (S A B C + - - S A B C)...
Qed.

Theorem para_not_col_1 :
 forall A B C D : Point, 
 parallelogram A B C D -> 
 ~ Col A B D. 
Proof.
unfold parallelogram, parallel, S4 in |- *.
intros.
decompose [and] H.
unfold not in |- *; intro.
rewrite H1 in H0.
NormalizeRing H0.
assert (Col A C B); Geometry.
Qed.

Hint Resolve para_not_col_1: Geom.

Theorem para_not_col_2 :
 forall A B C D : Point, parallelogram A B C D -> ~ Col B C D. 
Proof.
unfold parallelogram, parallel, S4 in |- *.
intros.
decompose [and] H.
unfold not in |- *; intro.
rewrite H1 in H2.
NormalizeRing H2.
assert (Col B A C); Geometry.
Qed.

Hint Resolve para_not_col_2: Geom.

Theorem para_not_col_3 :
 forall A B C D : Point, parallelogram A B C D -> ~ Col A C D. 
Proof.
unfold parallelogram, parallel,S4 in |- *.
intros.
decompose [and] H.
unfold not in |- *; intro.
assert (S A C D = S A C B + S A B D + S B C D); Geometry.
rewrite H1 in H4.
rewrite H0 in H4.
NormalizeRing H4.
assert (Col B C D); Geometry.
assert (parallelogram A B C D).
unfold parallelogram in |- *; auto.
assert (~ Col B C D); eauto with Geom.
Qed.

Hint Resolve para_not_col_3: Geom.

Theorem para_1 : forall A B C D : Point, 
  parallelogram A B C D -> parallelogram A D C B.
Proof.
unfold parallelogram in |- *.
intros.
decompose [and] H.
repeat split; Geometry.
assert (parallelogram A B C D); unfold parallelogram in |- *; auto.
assert (~ Col A C D); eauto with Geom.
Qed.

Hint Resolve para_1: Geom.

Theorem para_2 : forall A B C D : Point, 
  parallelogram A B C D -> parallelogram B A D C.
Proof.
intros.
assert (~Col A B D).
eauto with Geom.
unfold parallelogram in *.
intros.
decompose [and] H.
repeat split; Geometry.
Qed.

Hint Resolve para_2: Geom.

Theorem para_3 : forall A B C D : Point, 
  parallelogram A B C D -> parallelogram D C B A.
Proof.
intros.
assert (~Col D C B).
eauto with Geom.
unfold parallelogram in *.
intros.
decompose [and] H.
repeat split; Geometry.
Qed.

Hint Resolve para_3: Geom.

Theorem para_4 : forall A B C D : Point, 
  parallelogram A B C D -> parallelogram C B A D.
Proof.
intros.
assert (~Col D C B).
eauto with Geom.
unfold parallelogram in *.
intros.
decompose [and] H.
repeat split; Geometry.
Qed.

Hint Resolve para_4: Geom.

Theorem not_para_eq_1 : forall A B C, ~ parallelogram A A B C.
Proof.
intuition idtac.
assert (~ Col A A C).
eauto with Geom.
intuition.
Qed.

Theorem not_para_eq_2 : forall A B C, ~ parallelogram A B C C.
Proof.
intuition idtac.
assert (~ Col B C C).
eauto with Geom.
intuition.
Qed.

Theorem not_para_eq_3 : forall A B C, ~ parallelogram A B C A.
Proof.
intuition idtac.
assert (~ Col A B A).
eauto with Geom.
intuition.
Qed.

Theorem not_para_eq_4 : forall A B C, ~ parallelogram A B B C.
Proof.
intuition idtac.
assert (~ Col A B B).
eauto with Geom.
intuition.
Qed.

Theorem not_para_eq_5 : forall A B C, ~ parallelogram A B A C.
Proof.
intuition idtac.
assert (~ Col A A C).
eauto with Geom.
intuition.
Qed.

Theorem not_para_eq_6 : forall A B C, ~ parallelogram A B C B.
Proof.
intuition idtac.
assert (~ Col B C B).
eauto with Geom.
intuition.
Qed.

Hint Resolve not_para_eq_1 not_para_eq_2 not_para_eq_3 
not_para_eq_4 not_para_eq_5 not_para_eq_6 : Geom.

Theorem para_not_eq_1 : forall A B C D, 
  parallelogram A B C D -> A<>B.
Proof.
intros.
unfold not;intro.
subst A.
cut (~parallelogram B B C D).
auto.
Geometry.
Qed.

Theorem para_not_eq_2 : forall A B C D, 
  parallelogram A B C D -> A<>C.
Proof.
intros.
unfold not;intro.
subst A.
cut (~parallelogram C B C D).
auto.
Geometry.
Qed.

Theorem para_not_eq_3 : forall A B C D, 
  parallelogram A B C D -> A<>D.
Proof.
intros.
unfold not;intro.
subst A.
cut (~parallelogram D B C D).
auto.
Geometry.
Qed.

Theorem para_not_eq_4 : forall A B C D, 
  parallelogram A B C D -> B<>C.
Proof.
intros.
unfold not;intro.
subst B.
cut (~parallelogram A C C D).
auto.
Geometry.
Qed.

Theorem para_not_eq_5 : forall A B C D, 
  parallelogram A B C D -> B<>D.
Proof.
intros.
unfold not;intro.
subst B.
cut (~parallelogram A D C D).
auto.
Geometry.
Qed.

Theorem para_not_eq_6 : forall A B C D, 
  parallelogram A B C D -> C<>D.
Proof.
intros.
unfold not;intro.
subst C.
cut (~parallelogram A B D D).
auto.
Geometry.
Qed.

Hint Resolve para_not_eq_1 para_not_eq_2 para_not_eq_3
 para_not_eq_4 para_not_eq_5 para_not_eq_6 : Geom.

Theorem mid_point_equation :
 forall A C O P Q : Point,
 Col O A C -> A <> C -> A ** O = O ** C -> 
 S A P Q + S C P Q = 2 * S O P Q.
Proof.
intros.
assert (T := l2_9 P Q A C O H0 H).
rewrite H1 in T.
rewrite T.
assert (O ** C / A ** C = 1 / 2).
assert (A ** O + O ** C = A ** C); Geometry.
rewrite H1 in H2.
rewrite <- H2.
field.
assert (O ** C + O ** C = 2 * O ** C).
ring.
assert (O <> C).
unfold not in |- *; intro.
assert (O ** C = 0); Geometry.
rewrite H5 in H1.
assert (A = O); Geometry.
assert (A = C).
congruence.
auto.
rewrite H3; Geometry.
rewrite H2; field; Geometry.
Qed.

Theorem col_par_par :  forall A B C D D',  
 ~C=D -> 
 parallel A B C D -> 
 Col C D D' -> 
 parallel A B C D'.
Proof with Geometry.
intros.
cases_col A C D.
assert (Col C D B).
eapply par_col_col_1 with (C:=A)...
assert (Col C A B).
eapply col_trans_1 with (B:=D)...
assert (Col D A B).
eapply col_trans_1 with (B:=C)...
unfold parallel, S4 in *.
replace (S A C B) with (- S C A B).
rewrite H4.
ring_simplify.
assert (Col C A D').
eapply col_trans_1 with (B:=D)...
cases_equality A C.
subst A.
assert (Col C B D').
eapply col_trans_1 with (B:=D)...
auto.
assert (Col  A B D').
eapply col_trans_1 with (B:=C)...
auto.
Geometry.
assert (~Col B C D).
unfold not;intro.
assert (Col C D A).
eapply par_col_col_1 with (C:=B)...
assert (Col A C D)...

assert (parallel A B C D)...
unfold parallel, S4 in H4.
unfold parallel,S4.

assert (C**D' / C**D = S A C D' / S A C D).
apply A6...
assert (C**D' / C**D = S B C D' / S B C D).
apply A6...
set (C ** D' / C ** D) in *.

RewriteVar (S A C B) H4.

replace (S A B D) with (S A B C + S A C D + S C B D).
2:symmetry;Geometry.
replace (S A B D') with (S A B C + S A C D' + S C B D').
2:symmetry;Geometry.
ring_simplify.
RewriteVar (S A C D') H5.
2:Geometry.
replace (S C B D') with (- S B C D').
2:Geometry.
RewriteVar (S B C D') H6.
2:Geometry.
replace (S B C D) with (-S C B D).
2:Geometry.
ring_simplify.
replace ( S A C D * f - S A C D + S C B D * f - S C B D) with
((S A C D + S C B D) * (f-1)) by ring.
replace (S A C D + S C B D) with 0.
ring.

symmetry.
assert (parallel  C D B A)...
unfold parallel,S4 in *.
RewriteVar (S C B D) H9.
uniformize_signed_areas.
ring.
Qed.

