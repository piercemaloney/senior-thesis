
Require Import FunInd.
Require Export Qhomographic.
(* Qhomographic:
Require Export homographicAcc_Qhomographic_sign.
Require Import ZArithRing.

Lemma Qhomographic_sg_denom_nonzero_nonzero :
 forall (c d : Z) (p : Qpositive),
 c = 0%Z -> d = 0%Z -> ~ Qhomographic_sg_denom_nonzero c d p.

Lemma Qhomographic_sg_denom_nonzero_nonzero_2 :
 forall (c d : Z) (p : Qpositive),
 Qhomographic_sg_denom_nonzero c d p -> c <> d \/ c <> 0%Z.

Lemma Qhomographic_sg_denom_nonzero_nonzero_3 :
 forall (c d : Z) (p : Qpositive),
 Qhomographic_sg_denom_nonzero c d p -> c <> 0%Z \/ d <> 0%Z.

Definition Qhomographic_Qpositive_to_Q (a b c d : Z) 
  (p : Qpositive) (H_hsign : Qhomographic_sg_denom_nonzero c d p) : Q.

Inductive Qhomographic_denom_nonzero : Z -> Z -> Q -> Prop :=
  | Qhomographicok0 :
      forall (c d : Z) (s : Q),
      s = Zero -> d <> 0%Z -> Qhomographic_denom_nonzero c d s
  | Qhomographicok1 :
      forall (c d : Z) (s : Q) (p : Qpositive),
      s = Qpos p ->
      Qhomographic_sg_denom_nonzero c d p -> Qhomographic_denom_nonzero c d s
  | Qhomographicok2 :
      forall (c d : Z) (s : Q) (p : Qpositive),
      s = Qneg p ->
      Qhomographic_sg_denom_nonzero (- c) d p ->
      Qhomographic_denom_nonzero c d s.

Lemma Qhomographic_denom_nonzero_0 :
 forall c d : Z, Qhomographic_denom_nonzero c d Zero -> d <> 0%Z.  

Lemma Qhomographic_denom_nonzero_1 :
 forall (c d : Z) (p : Qpositive),
 Qhomographic_denom_nonzero c d (Qpos p) ->
 Qhomographic_sg_denom_nonzero c d p.

Lemma Qhomographic_denom_nonzero_2 :
 forall (c d : Z) (p : Qpositive),
 Qhomographic_denom_nonzero c d (Qneg p) ->
 Qhomographic_sg_denom_nonzero (- c) d p.

Definition Qhomographic (a b c d : Z) (s : Q)
  (H_Qhomographic_denom_nonzero : Qhomographic_denom_nonzero c d s) : Q.

Lemma Qhomographic_sg_denom_nonzero_always_1 :
 forall (d k k' : Z) (p : Qpositive),
 d <> 0%Z ->
 (0 < k)%Z -> (0 < k')%Z -> Qhomographic_sg_denom_nonzero (k * d) (k' * d) p.

Lemma Qhomographic_sg_denom_nonzero_Zero_always :
 forall (d : Z) (p : Qpositive),
 d <> 0%Z -> Qhomographic_sg_denom_nonzero 0 d p.

Lemma Qhomographic_sg_denom_nonzero_always_Zero :
 forall (c : Z) (p : Qpositive),
 c <> 0%Z -> Qhomographic_sg_denom_nonzero c 0 p.

Lemma always_nonzero_Qhomographic_sg_denom_nonzero :
 forall (d : Z) (p : Qpositive),
 Qhomographic_sg_denom_nonzero 0 d p -> d <> 0%Z.

Lemma Qhomographic_denom_nonzero_Zero_always :
 forall (d : Z) (s : Q), d <> 0%Z -> Qhomographic_denom_nonzero 0 d s.

Lemma Qhomographic_denom_nonzero_Zero_ONE :
 forall s : Q, Qhomographic_denom_nonzero 0 1 s.

Lemma Qhomographic_denom_nonzero_always_Zero :
 forall (c : Z) (s : Q) (H_nonzero_s : s <> Zero :>Q),
 c <> 0%Z -> Qhomographic_denom_nonzero c 0 s.

Lemma Qhomographic_denom_nonzero_ONE_Zero :
 forall (s : Q) (H_nonzero_s : s <> Zero :>Q),
 Qhomographic_denom_nonzero 1 0 s.

Definition Qhomographic_2x (x : Q) : Q :=
  Qhomographic 2 0 0 1 x (Qhomographic_denom_nonzero_Zero_ONE x).

Definition Qopp_lazy (x : Q) : Q :=
  Qhomographic (-1) 0 0 1 x (Qhomographic_denom_nonzero_Zero_ONE x).

Definition Qhomographic_kx (k : Z) (x : Q) : Q :=
  Qhomographic k 0 0 1 x (Qhomographic_denom_nonzero_Zero_ONE x).

Definition Qhomographic_x_over_k (k : Z) (Hk : k <> 0%Z) 
  (x : Q) : Q :=
  Qhomographic 1 0 0 k x (Qhomographic_denom_nonzero_Zero_always k x Hk).

Definition Qinv_lazy (x : Q) (Hx : x <> Zero) : Q :=
  Qhomographic 0 1 1 0 x (Qhomographic_denom_nonzero_ONE_Zero x Hx). *)

Functional Scheme fraction_encoding_ind := 
  Induction for fraction_encoding Sort Prop.

Lemma fraction_encoding_equal :
 forall (m n : Z) (Hn1 Hn2 : n <> 0%Z),
 fraction_encoding m n Hn1 = fraction_encoding m n Hn2.
Proof.
 intros m n Hn1.
 unfold fraction_encoding at 2; fold fraction_encoding. 
 functional induction (fraction_encoding m n Hn1); intros Hn2;
 rewrite e;
   trivial || (
 f_equal;
 apply positive_fraction_encoding_equal).
Defined. 

Ltac Rewrite_eq_ :=
  repeat
   match goal with
   | id1:(_ = left _ _) |- _ => rewrite id1; clear id1
   | id1:(_ = right _ _) |- _ => rewrite id1; clear id1
   | id1:(_ = inleft _ _) |- _ =>
       rewrite id1; clear id1
   | id1:(_ = inright _ _) |- _ => rewrite id1; clear id1
   end.

Ltac Clear_eq_ :=
  repeat
   match goal with
   | id1:(_ = left _ _) |- _ => clear id1
   | id1:(_ = right _ _) |- _ => clear id1
   | id1:(_ = inleft _ _) |- _ => clear id1
   | id1:(_ = inright _ _) |- _ => clear id1
   end.

Ltac Sign_mismatch :=
  Clear_eq_;
   match goal with
   | id1:(?X1 = 1%Z),id2:(?X1 = 0%Z) |- _ =>
       rewrite id2 in id1; discriminate id1
   | id1:(?X1 = 0%Z),id2:(?X1 = 1%Z) |- _ =>
       rewrite id2 in id1; discriminate id1
   | id1:(?X1 = (-1)%Z),id2:(?X1 = 0%Z) |- _ =>
       rewrite id2 in id1; discriminate id1
   | id1:(?X1 = 0%Z),id2:(?X1 = (-1)%Z) |- _ =>
       rewrite id2 in id1; discriminate id1
   | id1:(?X1 = 1%Z),id2:(?X1 = (-1)%Z) |- _ =>
       rewrite id2 in id1; discriminate id1
   | id1:(?X1 = (-1)%Z),id2:(?X1 = 1%Z) |- _ =>
       rewrite id2 in id1; discriminate id1
   end.

Functional Scheme Qhomographic_Qpositive_to_Q_ind := 
  Induction for Qhomographic_Qpositive_to_Q Sort Prop.

Lemma Qhomographic_Qpositive_to_Q_0 :
 forall (a b c d : Z) (p : Qpositive)
   (H_Qhomographic_sg_denom_nonzero : Qhomographic_sg_denom_nonzero c d p)
   (Hc : c <> 0%Z),
 (a * d)%Z = (b * c)%Z ->
 d = 0%Z ->
 Qhomographic_Qpositive_to_Q a b c d p H_Qhomographic_sg_denom_nonzero =
 fraction_encoding a c Hc. 
Proof.
 intros a b c d p H_Qhomographic_sg_denom_nonzero.
 functional induction ( Qhomographic_Qpositive_to_Q a b c d p H_Qhomographic_sg_denom_nonzero);
   intros;
     try solve [ Falsum ]; unfold Qhomographic_Qpositive_to_Q in |- *.
 apply fraction_encoding_equal.
 apply fraction_encoding_equal.
Defined.

Lemma Qhomographic_Qpositive_to_Q_1 :
 forall (a b c d : Z) (p : Qpositive)
   (H_Qhomographic_sg_denom_nonzero : Qhomographic_sg_denom_nonzero c d p)
   (Hd : d <> 0%Z),
 (a * d)%Z = (b * c)%Z ->
 Qhomographic_Qpositive_to_Q a b c d p H_Qhomographic_sg_denom_nonzero =
 fraction_encoding b d Hd. 
Proof.
  intros a b c d p H_Qhomographic_sg_denom_nonzero.
  functional induction (Qhomographic_Qpositive_to_Q a b c d p H_Qhomographic_sg_denom_nonzero);intros;
    try solve [ Falsum ]; unfold Qhomographic_Qpositive_to_Q in |- *.
      apply fraction_encoding_equal.
Defined.

Lemma Qhomographic_Qpositive_to_Q_2 :
 forall (a b c d : Z) (p : Qpositive)
   (H_Qhomographic_sg_denom_nonzero : Qhomographic_sg_denom_nonzero c d p),
 (a * d)%Z <> (b * c)%Z ->
 h_sign a b c d p H_Qhomographic_sg_denom_nonzero = 0%Z ->
 Qhomographic_Qpositive_to_Q a b c d p H_Qhomographic_sg_denom_nonzero = Zero.
Proof.
 intros a b c d p H_Qhomographic_sg_denom_nonzero.
 functional induction (Qhomographic_Qpositive_to_Q a b c d p H_Qhomographic_sg_denom_nonzero);intros;
   try solve [Falsum|Sign_mismatch];
   reflexivity.
Qed.

Ltac Order_mismatch :=
  Clear_eq_;
   match goal with
   | id1:(?X1 < ?X2)%Z,id2:(?X2 <= ?X1)%Z |- _ =>
       match goal with
       | id1:(?X1 < ?X2)%Z,id2:(?X2 <= ?X1)%Z |- _ =>
           first [ apply False_ind | apply False_rec ]; apply (Z.lt_irrefl X1);
            apply Z.lt_le_trans with X2; assumption
       end
   | id2:(?X1 <= ?X2)%Z,id1:(?X2 < ?X1)%Z |- _ =>
       match goal with
       | id1:(?X1 < ?X2)%Z,id2:(?X2 <= ?X1)%Z |- _ =>
           first [ apply False_ind | apply False_rec ]; apply (Z.lt_irrefl X1);
            apply Z.lt_le_trans with X2; assumption
       end
   end. 

Lemma Qhomographic_Qpositive_to_Q_3 :
 forall (a b c d : Z) (p : Qpositive)
   (H_Qhomographic_sg_denom_nonzero : Qhomographic_sg_denom_nonzero c d p),
 (a * d)%Z <> (b * c)%Z ->
 h_sign a b c d p H_Qhomographic_sg_denom_nonzero = 1%Z ->
 forall
   hyp_homographicAcc : homographicAcc
                          (new_a a b c d p H_Qhomographic_sg_denom_nonzero)
                          (new_b a b c d p H_Qhomographic_sg_denom_nonzero)
                          (new_c a b c d p H_Qhomographic_sg_denom_nonzero)
                          (new_d a b c d p H_Qhomographic_sg_denom_nonzero)
                          (new_p a b c d p H_Qhomographic_sg_denom_nonzero),
 (0 <
  Z.sgn
    (new_a a b c d p H_Qhomographic_sg_denom_nonzero +
     new_b a b c d p H_Qhomographic_sg_denom_nonzero))%Z ->
 Qhomographic_Qpositive_to_Q a b c d p H_Qhomographic_sg_denom_nonzero =
 Qpos
   (Qhomographic_Qpositive_to_Qpositive
      (new_a a b c d p H_Qhomographic_sg_denom_nonzero)
      (new_b a b c d p H_Qhomographic_sg_denom_nonzero)
      (new_c a b c d p H_Qhomographic_sg_denom_nonzero)
      (new_d a b c d p H_Qhomographic_sg_denom_nonzero)
      (new_p a b c d p H_Qhomographic_sg_denom_nonzero) hyp_homographicAcc).
Proof.
 intros a b c d p H_Qhomographic_sg_denom_nonzero.
 functional induction (Qhomographic_Qpositive_to_Q a b c d p H_Qhomographic_sg_denom_nonzero);intros; 
             try solve [ Falsum | Sign_mismatch | Order_mismatch ];
             unfold Qhomographic_Qpositive_to_Q in |- *; 
             apply f_equal with Qpositive;
             apply Qhomographic_Qpositive_to_Qpositive_equal.
Qed.

Lemma Qhomographic_Qpositive_to_Q_4 :
 forall (a b c d : Z) (p : Qpositive)
   (H_Qhomographic_sg_denom_nonzero : Qhomographic_sg_denom_nonzero c d p),
 (a * d)%Z <> (b * c)%Z ->
 h_sign a b c d p H_Qhomographic_sg_denom_nonzero = 1%Z ->
 forall
   hyp_homographicAcc : homographicAcc
                          (- new_a a b c d p H_Qhomographic_sg_denom_nonzero)
                          (- new_b a b c d p H_Qhomographic_sg_denom_nonzero)
                          (- new_c a b c d p H_Qhomographic_sg_denom_nonzero)
                          (- new_d a b c d p H_Qhomographic_sg_denom_nonzero)
                          (new_p a b c d p H_Qhomographic_sg_denom_nonzero),
 (Z.sgn
    (new_a a b c d p H_Qhomographic_sg_denom_nonzero +
     new_b a b c d p H_Qhomographic_sg_denom_nonzero) <= 0)%Z ->
 Qhomographic_Qpositive_to_Q a b c d p H_Qhomographic_sg_denom_nonzero =
 Qpos
   (Qhomographic_Qpositive_to_Qpositive
      (- new_a a b c d p H_Qhomographic_sg_denom_nonzero)
      (- new_b a b c d p H_Qhomographic_sg_denom_nonzero)
      (- new_c a b c d p H_Qhomographic_sg_denom_nonzero)
      (- new_d a b c d p H_Qhomographic_sg_denom_nonzero)
      (new_p a b c d p H_Qhomographic_sg_denom_nonzero) hyp_homographicAcc).
Proof.
 intros a b c d p H_Qhomographic_sg_denom_nonzero.
 functional induction 
   (Qhomographic_Qpositive_to_Q a b c d p H_Qhomographic_sg_denom_nonzero);intros;
   try solve [ Falsum | Sign_mismatch | Order_mismatch ];
     unfold Qhomographic_Qpositive_to_Q in |- *; 
       apply f_equal with Qpositive;
         apply Qhomographic_Qpositive_to_Qpositive_equal.
Qed.

Lemma Qhomographic_Qpositive_to_Q_5 :
 forall (a b c d : Z) (p : Qpositive)
   (H_Qhomographic_sg_denom_nonzero : Qhomographic_sg_denom_nonzero c d p),
 (a * d)%Z <> (b * c)%Z ->
 h_sign a b c d p H_Qhomographic_sg_denom_nonzero = (-1)%Z ->
 forall
   hyp_homographicAcc : homographicAcc
                          (- new_a a b c d p H_Qhomographic_sg_denom_nonzero)
                          (- new_b a b c d p H_Qhomographic_sg_denom_nonzero)
                          (new_c a b c d p H_Qhomographic_sg_denom_nonzero)
                          (new_d a b c d p H_Qhomographic_sg_denom_nonzero)
                          (new_p a b c d p H_Qhomographic_sg_denom_nonzero),
 (Z.sgn
    (new_a a b c d p H_Qhomographic_sg_denom_nonzero +
     new_b a b c d p H_Qhomographic_sg_denom_nonzero) < 0)%Z ->
 Qhomographic_Qpositive_to_Q a b c d p H_Qhomographic_sg_denom_nonzero =
 Qneg
   (Qhomographic_Qpositive_to_Qpositive
      (- new_a a b c d p H_Qhomographic_sg_denom_nonzero)
      (- new_b a b c d p H_Qhomographic_sg_denom_nonzero)
      (new_c a b c d p H_Qhomographic_sg_denom_nonzero)
      (new_d a b c d p H_Qhomographic_sg_denom_nonzero)
      (new_p a b c d p H_Qhomographic_sg_denom_nonzero) hyp_homographicAcc).
Proof.
 intros a b c d p H_Qhomographic_sg_denom_nonzero.
 functional induction 
   (Qhomographic_Qpositive_to_Q a b c d p H_Qhomographic_sg_denom_nonzero); 
   intros;
     try solve [ Falsum | Sign_mismatch | Order_mismatch ];
        (unfold Qhomographic_Qpositive_to_Q in |- *; 
         apply f_equal with Qpositive;
           apply Qhomographic_Qpositive_to_Qpositive_equal).
Qed.

Lemma Qhomographic_Qpositive_to_Q_6 :
 forall (a b c d : Z) (p : Qpositive)
   (H_Qhomographic_sg_denom_nonzero : Qhomographic_sg_denom_nonzero c d p),
 (a * d)%Z <> (b * c)%Z ->
 h_sign a b c d p H_Qhomographic_sg_denom_nonzero = (-1)%Z ->
 forall
   hyp_homographicAcc : homographicAcc
                          (new_a a b c d p H_Qhomographic_sg_denom_nonzero)
                          (new_b a b c d p H_Qhomographic_sg_denom_nonzero)
                          (- new_c a b c d p H_Qhomographic_sg_denom_nonzero)
                          (- new_d a b c d p H_Qhomographic_sg_denom_nonzero)
                          (new_p a b c d p H_Qhomographic_sg_denom_nonzero),
 (0 <=
  Z.sgn
    (new_a a b c d p H_Qhomographic_sg_denom_nonzero +
     new_b a b c d p H_Qhomographic_sg_denom_nonzero))%Z ->
 Qhomographic_Qpositive_to_Q a b c d p H_Qhomographic_sg_denom_nonzero =
 Qneg
   (Qhomographic_Qpositive_to_Qpositive
      (new_a a b c d p H_Qhomographic_sg_denom_nonzero)
      (new_b a b c d p H_Qhomographic_sg_denom_nonzero)
      (- new_c a b c d p H_Qhomographic_sg_denom_nonzero)
      (- new_d a b c d p H_Qhomographic_sg_denom_nonzero)
      (new_p a b c d p H_Qhomographic_sg_denom_nonzero) hyp_homographicAcc).
Proof.
 intros a b c d p H_Qhomographic_sg_denom_nonzero.
 functional induction 
   (Qhomographic_Qpositive_to_Q a b c d p H_Qhomographic_sg_denom_nonzero);intros;
             try solve [ Falsum | Sign_mismatch | Order_mismatch ];
             unfold Qhomographic_Qpositive_to_Q in |- *; 
             apply f_equal with Qpositive;
             apply Qhomographic_Qpositive_to_Qpositive_equal.
Qed.

Lemma h_sign_equal :
 forall (a b c d : Z) (p : Qpositive)
   (H_Qhomographic_sg_denom_nonzero1
    H_Qhomographic_sg_denom_nonzero2 : Qhomographic_sg_denom_nonzero c d p),
 h_sign a b c d p H_Qhomographic_sg_denom_nonzero1 =
 h_sign a b c d p H_Qhomographic_sg_denom_nonzero2.
Proof.
 abstract (intros a b c d p H_Qhomographic_sg_denom_nonzero1
            H_Qhomographic_sg_denom_nonzero2;
            change
              (fst
                 (Qhomographic_sign a b c d p
                    H_Qhomographic_sg_denom_nonzero1) =
               fst
                 (Qhomographic_sign a b c d p
                    H_Qhomographic_sg_denom_nonzero2)) 
             in |- *;
            apply f_equal with (Z * (Z * (Z * (Z * Z)) * Qpositive))%type;
            apply Qhomographic_sign_equal).
Defined.

Lemma new_a_equal :
 forall (a b c d : Z) (p : Qpositive)
   (H_Qhomographic_sg_denom_nonzero1
    H_Qhomographic_sg_denom_nonzero2 : Qhomographic_sg_denom_nonzero c d p),
 new_a a b c d p H_Qhomographic_sg_denom_nonzero1 =
 new_a a b c d p H_Qhomographic_sg_denom_nonzero2.
Proof.
 abstract (intros a b c d p H_Qhomographic_sg_denom_nonzero1
            H_Qhomographic_sg_denom_nonzero2; unfold new_a in |- *;
            apply f_equal with (Z * (Z * (Z * Z)))%type;
            apply f_equal with (Z * (Z * (Z * Z)) * Qpositive)%type;
            apply f_equal with (Z * (Z * (Z * (Z * Z)) * Qpositive))%type;
            apply Qhomographic_sign_equal).
Defined.

Lemma new_b_equal :
 forall (a b c d : Z) (p : Qpositive)
   (H_Qhomographic_sg_denom_nonzero1
    H_Qhomographic_sg_denom_nonzero2 : Qhomographic_sg_denom_nonzero c d p),
 new_b a b c d p H_Qhomographic_sg_denom_nonzero1 =
 new_b a b c d p H_Qhomographic_sg_denom_nonzero2.
Proof.
 abstract (intros a b c d p H_Qhomographic_sg_denom_nonzero1
            H_Qhomographic_sg_denom_nonzero2; unfold new_b in |- *;
            apply f_equal with (Z * (Z * Z))%type;
            apply f_equal with (Z * (Z * (Z * Z)))%type;
            apply f_equal with (Z * (Z * (Z * Z)) * Qpositive)%type;
            apply f_equal with (Z * (Z * (Z * (Z * Z)) * Qpositive))%type;
            apply Qhomographic_sign_equal).
Defined.

Lemma new_c_equal :
 forall (a b c d : Z) (p : Qpositive)
   (H_Qhomographic_sg_denom_nonzero1
    H_Qhomographic_sg_denom_nonzero2 : Qhomographic_sg_denom_nonzero c d p),
 new_c a b c d p H_Qhomographic_sg_denom_nonzero1 =
 new_c a b c d p H_Qhomographic_sg_denom_nonzero2.
Proof.
 abstract (intros a b c d p H_Qhomographic_sg_denom_nonzero1
            H_Qhomographic_sg_denom_nonzero2; unfold new_c in |- *;
            apply f_equal with (Z * Z)%type;
            apply f_equal with (Z * (Z * Z))%type;
            apply f_equal with (Z * (Z * (Z * Z)))%type;
            apply f_equal with (Z * (Z * (Z * Z)) * Qpositive)%type;
            apply f_equal with (Z * (Z * (Z * (Z * Z)) * Qpositive))%type;
            apply Qhomographic_sign_equal).
Defined.

Lemma new_d_equal :
 forall (a b c d : Z) (p : Qpositive)
   (H_Qhomographic_sg_denom_nonzero1
    H_Qhomographic_sg_denom_nonzero2 : Qhomographic_sg_denom_nonzero c d p),
 new_d a b c d p H_Qhomographic_sg_denom_nonzero1 =
 new_d a b c d p H_Qhomographic_sg_denom_nonzero2.
Proof.
 abstract (intros a b c d p H_Qhomographic_sg_denom_nonzero1
            H_Qhomographic_sg_denom_nonzero2; unfold new_d in |- *;
            apply f_equal with (Z * Z)%type;
            apply f_equal with (Z * (Z * Z))%type;
            apply f_equal with (Z * (Z * (Z * Z)))%type;
            apply f_equal with (Z * (Z * (Z * Z)) * Qpositive)%type;
            apply f_equal with (Z * (Z * (Z * (Z * Z)) * Qpositive))%type;
            apply Qhomographic_sign_equal).
Defined.

Lemma new_p_equal :
 forall (a b c d : Z) (p : Qpositive)
   (H_Qhomographic_sg_denom_nonzero1
    H_Qhomographic_sg_denom_nonzero2 : Qhomographic_sg_denom_nonzero c d p),
 new_p a b c d p H_Qhomographic_sg_denom_nonzero1 =
 new_p a b c d p H_Qhomographic_sg_denom_nonzero2.
Proof.
 abstract (intros a b c d p H_Qhomographic_sg_denom_nonzero1
            H_Qhomographic_sg_denom_nonzero2; unfold new_p in |- *;
            apply f_equal with (Z * (Z * (Z * Z)) * Qpositive)%type;
            apply f_equal with (Z * (Z * (Z * (Z * Z)) * Qpositive))%type;
            apply Qhomographic_sign_equal).
Defined.

Lemma Qhomographic_Qpositive_to_Q_equal :
 forall (a b c d : Z) (p : Qpositive)
   (H_Qhomographic_sg_denom_nonzero1
    H_Qhomographic_sg_denom_nonzero2 : Qhomographic_sg_denom_nonzero c d p),
 Qhomographic_Qpositive_to_Q a b c d p H_Qhomographic_sg_denom_nonzero1 =
 Qhomographic_Qpositive_to_Q a b c d p H_Qhomographic_sg_denom_nonzero2.
Proof.
 intros a b c d p H_Qhomographic_sg_denom_nonzero1.

 functional induction 
   (Qhomographic_Qpositive_to_Q a b c d p H_Qhomographic_sg_denom_nonzero1);intros.

generalize
 (Qhomographic_sg_denom_nonzero_nonzero_3 _ _ _
    H_Qhomographic_sg_denom_nonzero1).
 intros [Hc| Hd].

 rewrite
  (Qhomographic_Qpositive_to_Q_0 a b c 0 p H_Qhomographic_sg_denom_nonzero2
     Hc _x (refl_equal 0%Z)).
 apply fraction_encoding_equal.
 Falsum.
generalize
 (Qhomographic_sg_denom_nonzero_nonzero_3 _ _ _
    H_Qhomographic_sg_denom_nonzero1).
 intros [Hc| Hd].

 rewrite
  (Qhomographic_Qpositive_to_Q_0 a b c 0 p H_Qhomographic_sg_denom_nonzero2
     Hc _x (refl_equal 0%Z)).
 apply fraction_encoding_equal.
 Falsum.

 rewrite
  (Qhomographic_Qpositive_to_Q_1 a b c d p H_Qhomographic_sg_denom_nonzero2
     d_neq_zero _x).
 apply fraction_encoding_equal.
 
 clear e0.
 rewrite
  (h_sign_equal a b c d p H_Qhomographic_sg_denom_nonzero1
     H_Qhomographic_sg_denom_nonzero2) in _x.
 rewrite
  (Qhomographic_Qpositive_to_Q_2 a b c d p H_Qhomographic_sg_denom_nonzero2
     ad_neq_bc _x).
 reflexivity.
 
 Clear_eq_.
 generalize
  (Qhomographic_Qpositive_to_Q_homographicAcc_pos_1 a b c d p
     H_Qhomographic_sg_denom_nonzero1 ad_neq_bc l1_eq_one z).
 assert (l1_eq_one0 := l1_eq_one).
 rewrite
  (h_sign_equal a b c d p H_Qhomographic_sg_denom_nonzero1
     H_Qhomographic_sg_denom_nonzero2) in l1_eq_one0.
 change
   (0 <
    Z.sgn
      (new_a a b c d p H_Qhomographic_sg_denom_nonzero1 +
       new_b a b c d p H_Qhomographic_sg_denom_nonzero1))%Z 
  in z. 
 assert (z0 := z).
 rewrite (new_a_equal a b c d p H_Qhomographic_sg_denom_nonzero1
     H_Qhomographic_sg_denom_nonzero2) in z0.
 rewrite
  (new_b_equal a b c d p H_Qhomographic_sg_denom_nonzero1
     H_Qhomographic_sg_denom_nonzero2) in z0.
 rewrite
  (Qhomographic_Qpositive_to_Q_3 a b c d p H_Qhomographic_sg_denom_nonzero2
     ad_neq_bc l1_eq_one0
     (Qhomographic_Qpositive_to_Q_homographicAcc_pos_1 a b c d p
        H_Qhomographic_sg_denom_nonzero2 ad_neq_bc l1_eq_one0 z0) z0)
  .
 intro H_any_homographicAcc.
 apply f_equal with Qpositive.
 apply Qhomographic_Qpositive_to_Qpositive_equal_strong;
  [ rewrite
     (new_a_equal a b c d p H_Qhomographic_sg_denom_nonzero1
        H_Qhomographic_sg_denom_nonzero2)
  | rewrite
     (new_b_equal a b c d p H_Qhomographic_sg_denom_nonzero1
        H_Qhomographic_sg_denom_nonzero2)
  | rewrite
     (new_c_equal a b c d p H_Qhomographic_sg_denom_nonzero1
        H_Qhomographic_sg_denom_nonzero2)
  | rewrite
     (new_d_equal a b c d p H_Qhomographic_sg_denom_nonzero1
        H_Qhomographic_sg_denom_nonzero2)
  | rewrite
     (new_p_equal a b c d p H_Qhomographic_sg_denom_nonzero1
        H_Qhomographic_sg_denom_nonzero2) ]; reflexivity.

 Clear_eq_.
 generalize
  (Qhomographic_Qpositive_to_Q_homographicAcc_pos_2 a b c d p
     H_Qhomographic_sg_denom_nonzero1 ad_neq_bc l1_eq_one z).
 assert (l1_eq_one0 := l1_eq_one).
 rewrite
  (h_sign_equal a b c d p H_Qhomographic_sg_denom_nonzero1
     H_Qhomographic_sg_denom_nonzero2) in l1_eq_one0.
 change
   (Z.sgn
      (new_a a b c d p H_Qhomographic_sg_denom_nonzero1 +
       new_b a b c d p H_Qhomographic_sg_denom_nonzero1) <= 0)%Z 
  in z. 
 assert (z0 := z).
 rewrite
  (new_a_equal a b c d p H_Qhomographic_sg_denom_nonzero1
     H_Qhomographic_sg_denom_nonzero2) in z0.
 rewrite
  (new_b_equal a b c d p H_Qhomographic_sg_denom_nonzero1
     H_Qhomographic_sg_denom_nonzero2) in z0.
 rewrite
  (Qhomographic_Qpositive_to_Q_4 a b c d p H_Qhomographic_sg_denom_nonzero2
     ad_neq_bc l1_eq_one0
     (Qhomographic_Qpositive_to_Q_homographicAcc_pos_2 a b c d p
        H_Qhomographic_sg_denom_nonzero2 ad_neq_bc l1_eq_one0 z0) z0)
  .
 intro H_any_homographicAcc.
 apply f_equal with Qpositive.
 apply Qhomographic_Qpositive_to_Qpositive_equal_strong;
  [ rewrite
     (new_a_equal a b c d p H_Qhomographic_sg_denom_nonzero1
        H_Qhomographic_sg_denom_nonzero2)
  | rewrite
     (new_b_equal a b c d p H_Qhomographic_sg_denom_nonzero1
        H_Qhomographic_sg_denom_nonzero2)
  | rewrite
     (new_c_equal a b c d p H_Qhomographic_sg_denom_nonzero1
        H_Qhomographic_sg_denom_nonzero2)
  | rewrite
     (new_d_equal a b c d p H_Qhomographic_sg_denom_nonzero1
        H_Qhomographic_sg_denom_nonzero2)
  | rewrite
     (new_p_equal a b c d p H_Qhomographic_sg_denom_nonzero1
        H_Qhomographic_sg_denom_nonzero2) ]; reflexivity.
 
 Clear_eq_.
 generalize
  (Qhomographic_Qpositive_to_Q_homographicAcc_neg_1 a b c d p
     H_Qhomographic_sg_denom_nonzero1 ad_neq_bc l1_eq__minus_one z).
 assert (l1_eq__minus_one0 := l1_eq__minus_one).
 rewrite
  (h_sign_equal a b c d p H_Qhomographic_sg_denom_nonzero1
     H_Qhomographic_sg_denom_nonzero2) in l1_eq__minus_one0.
 change
   (Z.sgn
      (new_a a b c d p H_Qhomographic_sg_denom_nonzero1 +
       new_b a b c d p H_Qhomographic_sg_denom_nonzero1) < 0)%Z 
  in z. 
 assert (z0 := z).
 rewrite
  (new_a_equal a b c d p H_Qhomographic_sg_denom_nonzero1
     H_Qhomographic_sg_denom_nonzero2) in z0.
 rewrite
  (new_b_equal a b c d p H_Qhomographic_sg_denom_nonzero1
     H_Qhomographic_sg_denom_nonzero2) in z0.
 rewrite
  (Qhomographic_Qpositive_to_Q_5 a b c d p H_Qhomographic_sg_denom_nonzero2
     ad_neq_bc l1_eq__minus_one0
     (Qhomographic_Qpositive_to_Q_homographicAcc_neg_1 a b c d p
        H_Qhomographic_sg_denom_nonzero2 ad_neq_bc l1_eq__minus_one0 z0) z0)
  .
 intro H_any_homographicAcc.
 apply f_equal with Qpositive.
 apply Qhomographic_Qpositive_to_Qpositive_equal_strong;
  [ rewrite
     (new_a_equal a b c d p H_Qhomographic_sg_denom_nonzero1
        H_Qhomographic_sg_denom_nonzero2)
  | rewrite
     (new_b_equal a b c d p H_Qhomographic_sg_denom_nonzero1
        H_Qhomographic_sg_denom_nonzero2)
  | rewrite
     (new_c_equal a b c d p H_Qhomographic_sg_denom_nonzero1
        H_Qhomographic_sg_denom_nonzero2)
  | rewrite
     (new_d_equal a b c d p H_Qhomographic_sg_denom_nonzero1
        H_Qhomographic_sg_denom_nonzero2)
  | rewrite
     (new_p_equal a b c d p H_Qhomographic_sg_denom_nonzero1
        H_Qhomographic_sg_denom_nonzero2) ]; reflexivity.
 
 Clear_eq_.
 generalize
  (Qhomographic_Qpositive_to_Q_homographicAcc_neg_2 a b c d p
     H_Qhomographic_sg_denom_nonzero1 ad_neq_bc l1_eq__minus_one z).
 assert (l1_eq__minus_one0 := l1_eq__minus_one).
 rewrite
  (h_sign_equal a b c d p H_Qhomographic_sg_denom_nonzero1
     H_Qhomographic_sg_denom_nonzero2) in l1_eq__minus_one0.
 change
   (0 <=
    Z.sgn
      (new_a a b c d p H_Qhomographic_sg_denom_nonzero1 +
       new_b a b c d p H_Qhomographic_sg_denom_nonzero1))%Z 
  in z. 
 assert (z0:=z).
 rewrite
  (new_a_equal a b c d p H_Qhomographic_sg_denom_nonzero1
     H_Qhomographic_sg_denom_nonzero2) in z0.
 rewrite
  (new_b_equal a b c d p H_Qhomographic_sg_denom_nonzero1
     H_Qhomographic_sg_denom_nonzero2) in z0.
 rewrite
  (Qhomographic_Qpositive_to_Q_6 a b c d p H_Qhomographic_sg_denom_nonzero2
     ad_neq_bc l1_eq__minus_one0
     (Qhomographic_Qpositive_to_Q_homographicAcc_neg_2 a b c d p
        H_Qhomographic_sg_denom_nonzero2 ad_neq_bc l1_eq__minus_one0 z0) z0)
  .
 intro H_any_homographicAcc.
 apply f_equal with Qpositive.
 apply Qhomographic_Qpositive_to_Qpositive_equal_strong;
  [ rewrite
     (new_a_equal a b c d p H_Qhomographic_sg_denom_nonzero1
        H_Qhomographic_sg_denom_nonzero2)
  | rewrite
     (new_b_equal a b c d p H_Qhomographic_sg_denom_nonzero1
        H_Qhomographic_sg_denom_nonzero2)
  | rewrite
     (new_c_equal a b c d p H_Qhomographic_sg_denom_nonzero1
        H_Qhomographic_sg_denom_nonzero2)
  | rewrite
     (new_d_equal a b c d p H_Qhomographic_sg_denom_nonzero1
        H_Qhomographic_sg_denom_nonzero2)
  | rewrite
     (new_p_equal a b c d p H_Qhomographic_sg_denom_nonzero1
        H_Qhomographic_sg_denom_nonzero2) ]; reflexivity.
Defined.

