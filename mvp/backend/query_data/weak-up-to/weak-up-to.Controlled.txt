
Require Export Theory.
(* Theory:
Require Export WeakMonotonic.
Set Implicit Arguments.

Section Global.

  Variables A X Y: Type.
  Variable TX: reduction_t A X.
  Variable TY: reduction_t A Y.
  
  Section MonotonicCorrect.

    Variable F: function X Y.
    Hypothesis HF: monotonic TX TY F.

    Variable R: relation2 X Y.
    Hypothesis HR: evolve TX TY R (F R).

    Let phi: forall n, evolve TX TY (UExp F R n) (UExp F R (S n)).

    Theorem monotonic_correct: simulation TX TY (UIter F R).

  End MonotonicCorrect.

  Section WeakMonotonicCorrect.

    Variable F: function X Y.
    Hypothesis HF: wmonotonic TX TY F.

    Variable R: relation2 X Y.
    Hypothesis HRt: simulation_t TX TY R.
    Hypothesis HRa: evolve_a TX TY R (F R).
    
    Let silent: forall n, simulation_t TX TY (UExp F R n).

    Lemma wmonotonic_correct_t: simulation_t TX TY (UIter F R).

    Let visible: forall n, evolve_a TX TY (UExp F R n) (UExp F R (S n)).

    Theorem wmonotonic_correct: simulation TX TY (UIter F R).

  End WeakMonotonicCorrect.

  Section UnifiedCorrect.

    Variables F G: function X Y.
    Hypothesis HF : monotonic TX TY F.
    Hypothesis HG : wmonotonic TX TY G.

    Variable R: relation2 X Y.

    Hypothesis HFG: contains F G.  

    Hypothesis HRt: evolve_t TX TY R (F R).
    Hypothesis HRa: evolve_a TX TY R (G R).

    Let pre_silent: forall n, evolve_t TX TY (UExp F R n) (UExp F R (S n)).
    Let silent: simulation_t TX TY (UIter F R).

    Let HFGn: forall n, incl (UExp F R n) (UExp G R n).

    Let pre_visible: forall n, evolve TX TY (UExp F R n) (UExp G R (S n)).
    Let visible: evolve_a TX TY (UIter F R) (UIter G R).
   
    Let HGFGn: forall n, incl (UExp (UIter G) (UIter G R) n) (UExp (UIter G) R (S n)).
    Let HGFG: eeq (UIter (UIter G) (UIter F R)) (UIter (UIter G) R).

    Theorem unified_correct: simulation TX TY (UIter (UIter G) R).

  End UnifiedCorrect.

  Section ControlledCorrect.

    Variable B: relation X.
    Hypothesis HB: controlled TX TY B.

    Variables F G: function X Y.
    Hypothesis HF : monotonic TX TY F.
    Hypothesis HG : wmonotonic TX TY G.

    Hypothesis HBF: transparent B F.
    Hypothesis HFG: contains F G. 
    Hypothesis HBG: contains (chaining_l (star B)) G.

    Variable R: relation2 X Y.
    Hypothesis HRt: evolve_t TX TY R (comp (star B) (F R)).
    Hypothesis HRa: evolve_a TX TY R (G R).

    Let pre_silent: forall n, evolve_t TX TY (UExp F R n) (comp (star B) (UExp F R (S n))).
    Let silent: simulation_t TX TY (comp (star B) (UIter F R)).
    
    Let HFGn: forall n, incl (UExp F R n) (UExp G R n).
    Let HBGGn: forall R n, incl (comp (star B) (UExp G R n)) (UExp G R (S n)).

    Let pre_visible: forall n, evolve TX TY (UExp F R n) (UExp G R (2+n)).
    Let visible: evolve_a TX TY (comp (star B) (UIter F R)) (UIter G (comp (star B) (UIter F R))).
    
    Let HGGBF: eeq (UIter (UIter G) (comp (star B) (UIter F R))) (UIter (UIter G) R).

    Theorem controlled_correct: simulation TX TY (UIter (UIter G) R).

  End ControlledCorrect.

End Global. *)
Set Implicit Arguments.

Section RelaxedExpansion.
  
  Variables A X Y: Type.
  Variable TX: reduction_t A X.
  Variable TY: reduction_t A Y.
  
  Variable B: relation X.
  Hypothesis HB: wexpansion1 TX TX B.

  Let wexpansion1_ctrl_t: 
  forall R, evolve_t TX TY R (comp (star B) R) -> simulation_t TX TY (comp (star B) R).
  Proof.
    intros R HR x x' y Hxx' xRy; destruct xRy as [ w xRw wRy ].
    cgen Hxx'; cgen x'; induction xRw as [ x | z x w xRz zRw IH ]; intros x' Hxx'.
    apply (HR _ _ _ Hxx' wRy).
    destruct (HB Hxx' xRz) as [ z' Hzz' x'Rz' ].
    celim Hzz'; intro Hzz'.
    destruct Hzz'; exists y; auto; exists w; auto; apply star_trans with z; auto.
    destruct (IH wRy _ Hzz') as [ y' Hyy' z'Ry' ] ; destruct z'Ry' as [ w' ].
    exists y'; auto; exists w'; auto; apply S_star with z'; auto.
  Qed.
  
  Theorem wexpansion1_ctrl: wexpansion1 TX TX B -> controlled TX TY B.
  Proof.
    split; auto.
    intros R S HR HS HRS HRS' a x x' y Hxx' xRy; destruct xRy as [ w xRw wRy ].
    cgen Hxx'; cgen x'; induction xRw as [ x | z x w xRz zRw IH ]; intros x' Hxx'.
    destruct (HRS _ _ _ _ Hxx' wRy) as [ y' Hyy' w'Ry' ]; exists y'; auto; exists x'; auto.
    destruct (HB Hxx' xRz) as [ z' Hzz' x'Rz' ].
    destruct Hzz' as [ z1 Hzz1 Hz1z' ].
    destruct (IH wRy _ Hzz1) as [ y1 Hyy1 z1Ry1 ].
    cut (simulation_t TX TY (comp (star B) S)).
    intro HS'; destruct (weak_strong_t HS' _ Hz1z' z1Ry1) as [ y' Hy1y' z'Ry' ]; exists y'.
    apply weak_taus with y1; auto.
    destruct z'Ry' as [ w' ]; exists w'; auto; apply S_star with z'; auto.
    apply wexpansion1_ctrl_t; auto.
    unfold evolve_t; eapply evolve_incl; try apply HS; intros u v K; exists u; auto.
  Qed.

End RelaxedExpansion.

Section PlusWf.
  
  Variable A: Type.
  Variable X: Set.
  Variable Y: Type.
  Variable TX: reduction_t A X.
  Variable TY: reduction_t A Y.
  
  Variable B: relation X.
  Hypothesis HB: evolve TX TX B (plus B).
  Hypothesis HB': well_founded (trans B).

  Theorem plus_wf_controlled: controlled TX TY B.
  Proof.
    split.
    intros R HR x x' y Hxx' xRy; destruct xRy as [ w xRw wRy ]; destar xRw z.
    apply (HR _ _ _ Hxx' wRy).
    elim (diagram_plus_wf_2 (HB (l:=T _)) HB') with (eq (A:=X)) Y R R (TY (T A)) (eq (A:=Y)) x x' y; auto.
    intros y' Hyy' x'Ry'; exists y'; auto; destruct Hyy' as [ y1 Hyy1 Hy1y' ]; destruct Hy1y'; auto.
    intros u u' v Huu' uRv; destruct Huu'; exists v; auto; exists v; auto.
    intros u u' v Huu' uRv; destruct Huu'; exists v; auto; (exists v || exists u); auto.
    exists x'; auto.
    exists w; auto.

    intros R S HR HS HRS HRS' a x x' y Hxx' xRy. 
    elim (diagram_plus_wf_2 (HB (l:=T _)) HB') with 
      (comp (TX (L a)) (star (TX (T _)))) Y R S (TY (T _))
      (comp (TY (L a)) (star (TY (T _)))) x x' y; auto.
    intros y' Hyy' x'Ry'; exists y'; auto.
    clear Hxx' xRy x x' y; intros x x' y Hxx' xRy; destruct Hxx' as [ x1 Hxx1 Hx1x' ].
    destruct (HB Hxx1 xRy) as [ y1 Hyy1 x1Ry1 ].
    destruct (diagram_plus_wf (HB (l:=T _)) HB' Hx1x' x1Ry1) as [ y' Hy1y' x'Ry' ].
    exists y'; auto; fold (Weak TX (L a)); apply weak_taus with y1; auto.  
    clear Hxx' xRy x x' y; intros x x' y Hxx' xRy; destruct Hxx' as [ x1 Hxx1 Hx1x' ].
    destruct (HRS _ _ _ _ Hxx1 xRy) as [ y1 Hyy1 x1Ry1 ].
    destruct (weak_strong_t HS _ Hx1x' x1Ry1) as [ y' Hy1y' x'Ry' ]; exists y'.
    fold (Weak TY (L a)); apply weak_taus with y1; auto.  
    exists x'; auto.
    exists x; auto; exists x'; auto.
  Qed.

End PlusWf.

Section StarWf.
  
  Variable A: Type.
  Variable X: Set.
  Variable Y: Type.
  Variable TX: reduction_t A X.
  Variable TY: reduction_t A Y.
  
  Variable B: relation X.
  Hypothesis HB: evolve TX TX B (star B).
  Hypothesis HB': well_founded (trans (comp (plus B) (plus (TX (T _))))).

  Theorem star_wf_controlled: controlled TX TY B.
  Proof.
    split.
    intros R HR; unfold simulation_t, evolve_t, evolve_1, Weak.
    apply diagram_reverse; apply diagram_incl with (star (TX (T _))) (star (TY (T _))); auto; apply diagram_reverse.
    apply diagram_star_wf_1; auto; exact (HB (l:=T _)).

    intros R S HR HS HRS HRS' a; unfold evolve_1.
    apply diagram_reverse; apply diagram_incl with (Weak TX (L a)) (Weak TY (L a)); auto; apply diagram_reverse.
    unfold Weak; apply diagram_star_wf_2; auto; try exact (HB (l:=T _)); 
      intros x x' y Hxx' xRy; destruct Hxx' as [ x1 Hxx1 Hx1x' ]. 
    destruct (HB Hxx1 xRy) as [ y1 Hyy1 x1Ry1 ].
    destruct (diagram_star_wf (HB (l:=T _)) HB' Hx1x' x1Ry1) as [ y' Hy1y' x'Ry' ].
    exists y'; auto; fold (Weak TX (L a)); apply weak_taus with y1; auto.  
    destruct (HRS _ _ _ _ Hxx1 xRy) as [ y1 Hyy1 x1Ry1 ].
    destruct (weak_strong_t HS _ Hx1x' x1Ry1) as [ y' Hy1y' x'Ry' ]; exists y'.
    fold (Weak TY (L a)); apply weak_taus with y1; auto.  
    exists x'; auto.
  Qed.

End StarWf.

