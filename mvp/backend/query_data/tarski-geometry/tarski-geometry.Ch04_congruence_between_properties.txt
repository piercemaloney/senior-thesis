
Require Export  Ch03_between_properties.
(* Ch03_between_properties:
Require Export Ch02_congruence_properties.

Lemma beetween_trivial : forall A B : Point, Bet A B B.

Hint Resolve beetween_trivial : Tarski.

Lemma between_symmetry : forall A B C : Point, 
   Bet A B C -> Bet C B A.

Hint Resolve between_symmetry : sTarski.

Lemma beetween_trivial2 : forall A B : Point, Bet A A B.

Hint Resolve beetween_trivial beetween_trivial2 : Tarski.

Lemma between_egality : forall A B C : Point, Bet A B C -> Bet B A C -> A = B.

Hint Resolve between_egality : Tarski.

Lemma between_inner_transitivity : forall A B C D,
Bet A B D -> Bet B C D -> Bet A B C.

Lemma between_exchange3 : forall A B C D,
Bet A B C -> Bet A C D -> Bet B C D.

Hint Resolve between_exchange3 : Tarski.

Lemma outer_transitivity_between2 : forall A B C D,
Bet A B C -> Bet B C D -> B<>C -> Bet A C D.

Lemma between_exchange2 : forall A B C D,
Bet A B D -> Bet B C D -> Bet A C D.

Lemma outer_transitivity_between : forall A B C D,
Bet A B C -> Bet B C D -> B<>C -> Bet A B D.

Lemma between_exchange4 : forall A B C D,
Bet A B C -> Bet A C D -> Bet A B D.

Hint Resolve between_inner_transitivity : Tarski.
Hint Resolve between_exchange2 between_exchange3 between_exchange4 : Tarski.
Hint Resolve outer_transitivity_between outer_transitivity_between2 : Tarski.

Hint Resolve between_inner_transitivity 
 between_exchange2 between_exchange3 between_exchange4 
 outer_transitivity_between outer_transitivity_between2
 between_symmetry : Between.

Ltac Between := eauto with Between.

Definition Bet_4 := fun A1 A2 A3 A4 =>
   Bet A1 A2 A3 /\ Bet A2 A3 A4 /\ Bet A1 A3 A4 /\ Bet A1 A2 A4. 

Lemma l_3_9_4 : forall A1 A2 A3 A4, 
 Bet_4 A1 A2 A3 A4 -> Bet_4 A4 A3 A2 A1.

Lemma two_distinct_points : exists x, exists y: Point, x <> y.

Hint Resolve two_distinct_points : Tarski.

Lemma point_construction_different : forall A B,
  exists C, Bet A B C /\ B <> C.

Hint Resolve point_construction_different : Tarski.

Lemma another_point : forall A:Point, exists B, A<>B.

Lemma l3_17 : forall A B C A' B' P,
  Bet A B C ->  Bet A' B' C ->  Bet A P A' ->  
  exists Q, Bet P Q C /\ Bet B Q B'.

Definition IFSC := fun A B C D A' B' C' D' =>
   Bet A B C /\ Bet A' B' C' /\
   Cong A C A' C' /\ Cong B C B' C' /\ 
   Cong A D A' D' /\ Cong C D C' D'.

Ltac assert_cols := 
repeat
 match goal with
      | H:Bet ?X1 ?X2 ?X3 |- _ => not_exist_hyp (Col X1 X2 X3);assert (Col X1 X2 X3);[Tarski|idtac] 
 end.

Ltac clean_trivial_hyps :=
  repeat 
  match goal with
   |  H:(Cong ?X1 ?X1 ?X2 ?X2) |- _ => clear H
   |  H:(Cong ?X1 ?X2 ?X2 ?X1) |- _ => clear H
   |  H:(Cong ?X1 ?X2 ?X1 ?X2) |- _ => clear H
   |  H:(Bet ?X1 ?X1 ?X2) |- _ => clear H
   |  H:(Bet ?X2 ?X1 ?X1) |- _ => clear H
   |  H:(Col ?X1 ?X1 ?X2) |- _ => clear H
   |  H:(Col ?X2 ?X1 ?X1) |- _ => clear H
   |  H:(Col ?X1 ?X2 ?X1) |- _ => clear H
end.

Ltac smart_subst X := subst X;clean_trivial_hyps;clean_duplicated_hyps.

Ltac treat_equalities_aux := 
  match goal with
   |  H:(?X1 = ?X2) |- _ => smart_subst X2
end.

Ltac treat_equalities :=  
try treat_equalities_aux;
repeat
  match goal with
   |  H:(Cong ?X3 ?X3 ?X1 ?X2) |- _ => 
      assert (X1=X2);[apply cong_identity with (A:=X1) (B:=X2) (C:=X3);apply cong_symmetry;assumption|idtac];smart_subst X2
   |  H:(Cong ?X1 ?X2 ?X3 ?X3) |- _ => 
      assert (X1=X2);[apply cong_identity with (A:=X1) (B:=X2) (C:=X3);assumption|idtac];smart_subst X2
   |  H:(Bet ?X1 ?X2 ?X1) |- _ => 
      assert (X1=X2);[apply between_identity;assumption|idtac];smart_subst X2
end. *)

Lemma l4_2 : forall A B C D A' B' C' D',
  IFSC A B C D A' B' C' D' -> Cong B D B' D'.
Proof with Tarski.
intros.
unfold IFSC in H.
intuition.
cases_equality A C.

treat_equalities...

assert (exists E, Bet A C E /\ C <> E)...
DecompExAnd H6 E.

assert (exists E', Bet A' C' E' /\ Cong C' E' C E)...
DecompExAnd H6 E'.
assert (Cong E D E' D').
eapply five_segments_with_def.

assert (OFSC A C E D A' C' E' D').
unfold OFSC.
intuition.
apply H6.
assumption.

assert (OFSC E C B D E' C' B' D').
unfold OFSC.
intuition;esTarski.
esTarski.
Qed.

Lemma l4_3 :  forall A B C A' B' C',
  Bet A B C -> Bet A' B' C' ->
  Cong A C A' C' -> Cong B C B' C' -> Cong A B A' B'.
Proof with Tarski.
intros.
assert (IFSC A B C A A' B' C' A').
unfold IFSC;intuition.
apply cong_commutativity.
eapply l4_2.
apply H3.
Qed.

Hint Resolve l4_2 l4_3 : Tarski.

Definition Cong_3 := fun A1 A2 A3 B1 B2 B3 =>
  Cong A1 A2 B1 B2 /\ Cong A1 A3 B1 B3 /\ Cong A2 A3 B2 B3. 

Lemma l4_5 : forall A B C A' C',
  Bet A B C -> Cong A C A' C' ->
  exists B', Bet A' B' C' /\ Cong_3 A B C A' B' C'.
Proof with Tarski.
intros.
assert (exists D',  Bet C' A' D' /\ A' <> D')...
DecompExAnd H1 D'.
assert (exists B',  Bet D' A' B' /\ Cong A' B' A B)...
DecompExAnd H1 B'.
assert (exists C'',  Bet D' B' C'' /\ Cong B' C'' B C)...
DecompExAnd H1 C''.
assert (Bet A' B' C'');Between.
assert (Bet D' A' C'');Between.
assert (Cong A C A' C'').
eapply l2_11.
apply H.
apply H1.
sTarski.
sTarski.
assert (C''=C').
assert (D' <> A').
auto.
eapply construction_unicity.
apply H10.
eauto.
apply cong_symmetry.
apply H9.
sTarski.
sTarski.
exists B'.
unfold Cong_3.
intuition.

subst C'...

subst C''.
sTarski.
Qed.

Hint Resolve l4_5 : Tarski.

Lemma l4_6 : forall A B C A' B' C',
  Bet A B C -> Cong_3 A B C A' B' C' ->  Bet A' B' C'.
Proof with Tarski.
intros.
assert (exists B'', Bet A' B'' C' /\ Cong_3 A B C A' B'' C').
eapply l4_5...
unfold Cong_3 in H0;intuition...
DecompExAnd H1 B''.
assert (Cong_3 A' B'' C' A' B' C').
unfold Cong_3 in *;intuition;esTarski.
assert (IFSC A' B'' C' B'' A' B'' C' B');unfold IFSC;intuition.
unfold Cong_3 in *;intuition...
unfold Cong_3 in *;intuition...
assert (Cong B'' B'' B'' B').
eapply l4_2;eauto.
treat_equalities...
Qed.

Hint Unfold Col.

Lemma col_permutation_1 : forall A B C,
 Col A B C -> Col B C A.
Proof.
unfold Col.
intuition.
Qed.

Lemma col_permutation_2 : forall A B C,
 Col A B C -> Col C A B.
Proof.
unfold Col.
intuition.
Qed.

Lemma col_permutation_3 : forall A B C,
 Col A B C -> Col C B A.
Proof.
unfold Col.
intuition.
Qed.

Lemma col_permutation_4 : forall A B C,
 Col A B C -> Col B A C.
Proof.
unfold Col.
intuition.
Qed.

Lemma col_permutation_5 : forall A B C,
 Col A B C -> Col A C B.
Proof.
unfold Col.
intuition.
Qed.

Hint Resolve col_permutation_1 col_permutation_2 
col_permutation_3 col_permutation_4 col_permutation_5 : sTarski.

Hint Resolve col_permutation_1 col_permutation_2 
col_permutation_3 col_permutation_4 col_permutation_5 : Col.

Ltac Col := eauto with Col.

Lemma col_trivial_1 : forall A B, Col A A B.
Proof.
unfold Col.
intuition.
Qed.

Lemma col_trivial_2 : forall A B, Col A B B.
Proof.
unfold Col.
intuition.
Qed.

Lemma col_trivial_3 : forall A B, Col A B A.
Proof.
unfold Col.
intuition.
Qed.

Hint Resolve col_trivial_1 col_trivial_2 col_trivial_3 : Tarski.

Lemma l4_13 : forall A B C A' B' C',
  Col A B C -> Cong_3 A B C A' B' C' -> Col A' B' C'.
Proof.
unfold Col.
intros.
intuition.
left.
eapply l4_6;eauto.
right. 
left.
eapply l4_6;eauto.
unfold Cong_3 in *;intuition.
right.
right.
eapply l4_6;eauto.
unfold Cong_3 in *;intuition.
Qed.

Lemma l4_14 :  forall A B C A' B',
  Col A B C -> Cong A B A' B' ->
  exists C', Cong_3 A B C A' B' C'.
Proof with Tarski.
intros.
unfold Col in H;intuition.

assert (exists C', Bet A' B' C' /\ Cong B' C' B C)...
DecompExAnd H C'.
assert (Cong A C A' C').
eapply l2_11;esTarski.
exists C'...
unfold Cong_3;intuition...

Focus 2.
assert (exists C', Bet B' A' C' /\ Cong A' C' A C)...
DecompExAnd H1 C'.
exists C'...
unfold Cong_3;intuition...
eapply ( l2_11 B A C B' A' C');esTarski.

assert (exists B'0 : Point, Bet B' B'0 A' /\ Cong_3 B C A B' B'0 A').
eapply l4_5;esTarski.
DecompExAnd H1 C'.
exists C'...
unfold Cong_3 in *;intuition...
Qed.

Definition FSC := fun A B C D A' B' C' D' =>
  Col A B C /\ Cong_3 A B C A' B' C' /\ Cong A D A' D' /\ Cong B D B' D'.

Lemma l4_16 :  forall A B C D A' B' C' D', 
   FSC A B C D A' B' C' D' -> A<>B -> Cong C D C' D'.
Proof with Tarski.
unfold FSC.
intros.
intuition.
unfold Col in H1.
intuition.

assert (Bet A' B' C').
eapply l4_6; try apply H3;unfold Cong_3 in *;intuition...
unfold Cong_3 in H;intuition.
assert (A<>B);auto.
eapply five_segments;try apply H5;auto.

assert (Bet B' C' A').
apply (l4_6 B C A B' C' A')...
unfold Cong_3 in *;intuition.
unfold Cong_3 in H;intuition.
assert (IFSC A C B D A' C' B' D').
unfold IFSC;intuition.
eapply l4_2.
apply H6.

assert (Bet C' A' B').
apply (l4_6 C A B C' A' B')...
unfold Cong_3 in *;intuition.
unfold Cong_3 in H;intuition.

eapply five_segments.
2:apply H.
3:assumption.
3:apply between_symmetry;apply H1.
3:apply between_symmetry;apply H3.
sTarski.
sTarski.
auto.
Qed.

Lemma l4_17 :  forall A B C P Q,
  A<>B -> Col A B C -> 
  Cong A P A Q  -> Cong B P B Q -> Cong C P C Q.
Proof.
intros.
assert (FSC A B C P A B C Q);unfold FSC;unfold Cong_3;intuition.
eapply l4_16.
apply H3.
assumption.
Qed.

Lemma l4_18 : forall A B C C', 
  A<>B -> Col A B C -> 
  Cong A C A C'  -> Cong B C B C' -> C=C'.
Proof.
intros.
assert (Cong C C C C').
eapply l4_17;eauto.
treat_equalities.
auto.
Qed.

Lemma l4_19 : forall A B C C', 
 Bet A C B -> Cong A C A C' -> Cong B C B C' -> C=C'.
Proof with Tarski.
intros.
cases_equality A B.
treat_equalities...

assert (Col A B C);unfold Col;intuition...
eapply l4_18;eauto.
Qed.

