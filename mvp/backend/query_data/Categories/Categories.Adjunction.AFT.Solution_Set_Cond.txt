From Categories Require Import Essentials.Notations.
From Categories Require Import Essentials.Types.
From Categories Require Import Essentials.Facts_Tactics.
From Categories Require Import Category.Main.

Record Solution_Set_Cond (C : Category) :=
  {
    SSC_Type : Type;
    SSC_Objs : SSC_Type â†’ C;
    SSC_jointly_weakly_initial :>
      âˆ€ (c : C), {t : SSC_Type & ((SSC_Objs t) â€“â‰» c)%morphism}
  }
.

Arguments SSC_Type {_} _.
Arguments SSC_Objs {_} _ _.
Arguments SSC_jointly_weakly_initial {_} _ _.

From Categories Require Import Limits.Limit Limits.GenProd_GenSum.
From Categories Require Import Functor.Functor.
From Categories Require Import NatTrans.NatTrans.
From Categories Require Import
        Basic_Cons.Terminal
        Basic_Cons.Equalizer
        Basic_Cons.Limits
        Basic_Cons.Facts.Equalizer_Monic
.
From Categories Require Import Archetypal.Discr.Discr.

Section Complete_SSC_Initial.
  Context
    {C : Category}
    (CC : Complete C)
    (SSC : Solution_Set_Cond C)
  .

  Definition SSC_Prod : (Î  (SSC_Objs SSC))%object
    :=
      (LimitOf (Discr_Func (SSC_Objs SSC))).

  Definition SSC_Prod_WI (c : C) :
    (SSC_Prod â€“â‰» c)%morphism
    :=
      (
        (projT2 (SSC_jointly_weakly_initial SSC c))
          âˆ˜
          (
            Trans
              (cone_edge SSC_Prod)
              (projT1 (SSC_jointly_weakly_initial SSC c))
          )
      )%morphism
  .

  Definition endomorph_const (h : (SSC_Prod â€“â‰» SSC_Prod)%morphism) : C
    :=
      SSC_Prod
  .

  Definition Endo_Prod : (Î  endomorph_const)%object
    :=
      (LimitOf (Discr_Func endomorph_const)).

  Program Definition Cone_Endo_Prod_ids : Cone (Discr_Func endomorph_const)
    :=
      {|
        cone_apex :=
          {|
            FO := fun _ => SSC_Prod;
            FA := fun _ _ _ => id
          |};
        cone_edge :=
          {|
            Trans := fun _ => id
          |}
      |}
  .

  Definition morph_to_Endo_Prod_ids : (SSC_Prod â€“â‰» Endo_Prod)%morphism
    :=
      Trans (LRKE_morph_ex Endo_Prod Cone_Endo_Prod_ids) tt.

  Program Definition Cone_Endo_Prod_endomorphs :
    Cone (Discr_Func endomorph_const)
    :=
      {|
        cone_apex :=
          {|
            FO := fun _ => SSC_Prod;
            FA := fun _ _ _ => id
          |};
        cone_edge :=
          {|
            Trans := fun h => h
          |}
      |}
  .

  Definition morph_to_Endo_Prod_endomorphs : (SSC_Prod â€“â‰» Endo_Prod)%morphism
    :=
      Trans (LRKE_morph_ex Endo_Prod Cone_Endo_Prod_endomorphs) tt.

  Definition ids_endomorphs_equalizer :
    Equalizer
      morph_to_Endo_Prod_endomorphs
      morph_to_Endo_Prod_ids
    :=
      Equalizer_as_Limit
        morph_to_Endo_Prod_endomorphs
        morph_to_Endo_Prod_ids
        (LimitOf
           (Equalizer_Producing_Func
              morph_to_Endo_Prod_endomorphs
              morph_to_Endo_Prod_ids)
        )
  .

  Definition ids_endomorphs_equalizer_WI (c : C) :
    (ids_endomorphs_equalizer â€“â‰» c)%morphism
    :=
      (SSC_Prod_WI c âˆ˜ equalizer_morph ids_endomorphs_equalizer)%morphism
  .

  Theorem ids_endomorphs_equalizer_morph_neutralizes_endomorphs
          (d : (SSC_Prod â€“â‰» SSC_Prod)%morphism)
    :
      (d âˆ˜ equalizer_morph ids_endomorphs_equalizer)%morphism
      = equalizer_morph ids_endomorphs_equalizer
  .
  Proof.
    assert (H :=
              f_equal
                (fun w => ((Trans Endo_Prod d) âˆ˜ w)%morphism)
                (equalizer_morph_com ids_endomorphs_equalizer)
           ).
    cbn -[equalizer_morph ids_endomorphs_equalizer Endo_Prod] in H.
    unfold morph_to_Endo_Prod_endomorphs, morph_to_Endo_Prod_ids in H.
    repeat rewrite assoc_sym in H.
    assert (V :=
           f_equal
             (fun w :
                    ((Functor_Ops.Functor_compose
                        (Functor_To_1_Cat
                           (Discr_Cat (SSC_Prod â€“â‰» SSC_Prod)%morphism))
                        Cone_Endo_Prod_endomorphs)
                       â€“â‰» Discr_Func endomorph_const)%nattrans
              => Trans w d)
             (cone_morph_com
                (LRKE_morph_ex Endo_Prod Cone_Endo_Prod_endomorphs))
        ).
    cbn -[LRKE_morph_ex Endo_Prod] in V.
    rewrite From_Term_Cat in V.
    simpl_ids in V.
    rewrite <- V in H.
    clear V.
    assert (V :=
           f_equal
             (fun w :
                    ((Functor_Ops.Functor_compose
                       (Functor_To_1_Cat
                          (Discr_Cat (SSC_Prod â€“â‰» SSC_Prod)%morphism))
                       Cone_Endo_Prod_ids)
                       â€“â‰» Discr_Func endomorph_const)%nattrans
              => Trans w d)
             (cone_morph_com (LRKE_morph_ex Endo_Prod Cone_Endo_Prod_ids))
        ).
    cbn -[LRKE_morph_ex Endo_Prod] in V.
    rewrite From_Term_Cat in V.
    simpl_ids in V.
    rewrite <- V in H.
    clear V.
    auto.
  Qed.

  Section equalizer_of_morphs_from_ids_endomorphs_equalizer_iso.
    Context
      {d : C}
      (f g : (ids_endomorphs_equalizer â€“â‰» d)%morphism)
    .

    Definition equalizer_of_morphs_from_ids_endomorphs_equalizer
      :
        Equalizer f g
      :=
        Equalizer_as_Limit
          f
          g
          (LimitOf (Equalizer_Producing_Func f g))
    .

    Theorem equalizer_of_morphs_from_ids_endomorphs_equalizer_iso_RI :
      ((equalizer_morph (equalizer_of_morphs_from_ids_endomorphs_equalizer))
         âˆ˜
         ((SSC_Prod_WI _)
            âˆ˜ (equalizer_morph ids_endomorphs_equalizer)))%morphism
      =
      id.
    Proof.
      apply (
          mono_morphism_monomorphic
            (@Equalizer_Monic _ _ _ _ _ ids_endomorphs_equalizer)
        ).
      rewrite id_unit_right.
      repeat rewrite assoc_sym.
      apply ids_endomorphs_equalizer_morph_neutralizes_endomorphs.      
    Qed.

    Theorem equalizer_of_morphs_from_ids_endomorphs_equalizer_iso_LI :
      (((SSC_Prod_WI _) âˆ˜ (equalizer_morph ids_endomorphs_equalizer))
         âˆ˜
         (equalizer_morph (equalizer_of_morphs_from_ids_endomorphs_equalizer))
      )%morphism
      =
      id.
    Proof.
      apply (
          mono_morphism_monomorphic
            (@Equalizer_Monic _ _ _ _ _
                              equalizer_of_morphs_from_ids_endomorphs_equalizer)
        ).
      unfold Equalizer_Monic.
      cbn [mono_morphism].
      rewrite assoc_sym.
      simpl_ids.
      trivial.
      apply equalizer_of_morphs_from_ids_endomorphs_equalizer_iso_RI.
    Qed.
    
    Program Definition equalizer_of_morphs_from_ids_endomorphs_equalizer_iso
      :
        ((equalizer_of_morphs_from_ids_endomorphs_equalizer)
           â‰ƒ ids_endomorphs_equalizer)%isomorphism
      :=
        {|
          iso_morphism := equalizer_morph
                            (equalizer_of_morphs_from_ids_endomorphs_equalizer);
          inverse_morphism :=
            ((SSC_Prod_WI _)
               âˆ˜ (equalizer_morph ids_endomorphs_equalizer))%morphism;
          left_inverse :=
            equalizer_of_morphs_from_ids_endomorphs_equalizer_iso_LI;
          right_inverse :=
            equalizer_of_morphs_from_ids_endomorphs_equalizer_iso_RI
        |}
    .

  End equalizer_of_morphs_from_ids_endomorphs_equalizer_iso.

  Local Obligation Tactic := idtac.
  
  Program Definition Complete_SSC_Initial : (ðŸ˜_ C)%object
    :=
      {|
        terminal := ids_endomorphs_equalizer;
        t_morph := ids_endomorphs_equalizer_WI
      |}
  .

  Next Obligation.
  Proof.
    intros d f g.
    cbn -[ids_endomorphs_equalizer] in *.
    assert (H :=
              f_equal
                (fun w => (f âˆ˜ w)%morphism)
                (equalizer_of_morphs_from_ids_endomorphs_equalizer_iso_RI f g)).
    cbn -[ids_endomorphs_equalizer
            equalizer_of_morphs_from_ids_endomorphs_equalizer] in H.
    simpl_ids in H.
    rewrite <- H.
    clear H.
    assert (H :=
              f_equal
                (fun w => (g âˆ˜ w)%morphism)
                (equalizer_of_morphs_from_ids_endomorphs_equalizer_iso_RI f g)).
    cbn -[ids_endomorphs_equalizer
            equalizer_of_morphs_from_ids_endomorphs_equalizer] in H.
    simpl_ids in H.
    etransitivity; [|apply H].
    clear H.
    repeat rewrite assoc_sym.
    match goal with
      [|- (((f âˆ˜ ?A) âˆ˜ ?B) âˆ˜ ?C = _)%morphism] =>
      apply (f_equal (fun w => ((w âˆ˜ B) âˆ˜ C)%morphism))
    end.
    apply (equalizer_morph_com
             (equalizer_of_morphs_from_ids_endomorphs_equalizer f g)).
  Qed.

End Complete_SSC_Initial.
