
Require Import Classical.

Require Import Ensembles.
(* Ensembles:
Require Import Classical.

Section Ensembles.
   Variable U : Type.
   
   Definition Ensemble := U -> Prop.
   
   Definition In (B : Ensemble) (x : U) : Prop := B x.
   
   Definition Included (B C : Ensemble) : Prop :=
     forall x : U, In B x -> In C x.
   
   Inductive Empty_set : Ensemble :=.
   
   Inductive Singleton (x : U) : Ensemble :=
       In_singleton : In (Singleton x) x.
   
   Inductive Union (B C : Ensemble) : Ensemble :=
     | Union_introl : forall x : U, In B x -> In (Union B C) x
     | Union_intror : forall x : U, In C x -> In (Union B C) x.
   
   Inductive Intersection (B C : Ensemble) : Ensemble :=
       Intersection_intro :
         forall x : U, In B x -> In C x -> In (Intersection B C) x.
   
   Inductive Couple (x y : U) : Ensemble :=
     | Couple_l : In (Couple x y) x
     | Couple_r : In (Couple x y) y.
   
   Inductive Setminus (B C : Ensemble) : Ensemble :=
       Setminus_intro :
         forall x : U, In B x -> ~ In C x -> In (Setminus B C) x.
   
   Inductive Disjoint (B C : Ensemble) : Prop :=
       Disjoint_intro :
         (forall x : U, ~ In (Intersection B C) x) -> Disjoint B C.
   
   Inductive Non_empty (B : Ensemble) : Prop :=
       Non_empty_intro : forall x : U, In B x -> Non_empty B.
   
   Inductive Finite : Ensemble -> Prop :=
     | Empty_is_finite : Finite Empty_set
     | Union_is_finite :
         forall A : Ensemble,
         Finite A -> forall x : U, ~ In A x -> Finite (Union (Singleton x) A).

   Definition Same_set (B C : Ensemble) : Prop :=
     Included B C /\ Included C B.
      
   Lemma Same_set_intro :
    forall B C : Ensemble, Included B C -> Included C B -> Same_set B C.

   Axiom
     Extensionality_Ensembles :
       forall A B : Ensemble, Same_set A B -> A = B :>Ensemble.
   
End Ensembles.
Hint Unfold In.
Hint Unfold Included.
Hint Resolve Same_set_intro.
Hint Resolve Union_introl Union_intror.
Hint Resolve Intersection_intro.
Hint Resolve In_singleton.
Hint Resolve Couple_l Couple_r.
Hint Resolve Setminus_intro.
Hint Resolve Disjoint_intro.
Hint Resolve Empty_is_finite Union_is_finite.
Hint Resolve Extensionality_Ensembles. *)
Require Import Relations_1.
(* Relations_1:
Section Relations_1.
   Variable U : Type.
   
   Definition Relation := U -> U -> Prop.
   Variable R : Relation.
   
   Definition Reflexive : Prop := forall x : U, R x x.
   
   Definition Transitive : Prop := forall x y z : U, R x y -> R y z -> R x z.
   
   Definition Symmetric : Prop := forall x y : U, R x y -> R y x.
   
   Definition Antisymmetric : Prop :=
     forall x y : U, R x y -> R y x -> x = y :>U.
   
   Definition contains (R R' : Relation) : Prop :=
     forall x y : U, R' x y -> R x y.
   
   Definition same_relation (R R' : Relation) : Prop :=
     contains R R' /\ contains R' R.
   
   Inductive Preorder : Prop :=
       Definition_of_preorder : Reflexive -> Transitive -> Preorder.
   
   Inductive Order : Prop :=
       Definition_of_order :
         Reflexive -> Transitive -> Antisymmetric -> Order.
   
   Inductive Equivalence : Prop :=
       Definition_of_equivalence :
         Reflexive -> Transitive -> Symmetric -> Equivalence.
   
   Inductive PER : Prop :=
       Definition_of_PER : Symmetric -> Transitive -> PER.
   
End Relations_1.
Hint Unfold Reflexive.
Hint Unfold Transitive.
Hint Unfold Antisymmetric.
Hint Unfold Symmetric.
Hint Unfold contains.
Hint Unfold same_relation.
Hint Resolve Definition_of_preorder.
Hint Resolve Definition_of_order.
Hint Resolve Definition_of_equivalence.
Hint Resolve Definition_of_PER. *)
Require Import Relations_1_facts.
(* Relations_1_facts:
Require Import Relations_1.

Definition Complement (U : Type) (R : Relation U) : 
  Relation U := fun x y : U => ~ R x y.

Theorem Rsym_imp_notRsym :
 forall (U : Type) (R : Relation U),
 Symmetric U R -> Symmetric U (Complement U R).

Theorem Equiv_from_preorder :
 forall (U : Type) (R : Relation U),
 Preorder U R -> Equivalence U (fun x y : U => R x y /\ R y x).
intros U R H'; elim H'; intros H'0 H'1.
apply Definition_of_equivalence.
Hint Resolve Equiv_from_preorder.

Theorem Equiv_from_order :
 forall (U : Type) (R : Relation U),
 Order U R -> Equivalence U (fun x y : U => R x y /\ R y x).
Hint Resolve Equiv_from_order.

Theorem contains_is_preorder :
 forall U : Type, Preorder (Relation U) (contains U).
Hint Resolve contains_is_preorder.

Theorem same_relation_is_equivalence :
 forall U : Type, Equivalence (Relation U) (same_relation U).
Hint Resolve same_relation_is_equivalence. *)
Require Import podefs.
(* podefs:
Require Import Ensembles.
Require Import Relations_1.

Section Partial_orders.
   Variable U : Type.
   
   Definition Carrier := Ensemble U.
   
   Definition Rel := Relation U.
   
   Inductive PO : Type :=
       Definition_of_PO :
         forall (C : Carrier) (R : Rel), Non_empty U C -> Order U R -> PO.
   
   Theorem Carrier_of : PO -> Carrier.

   Theorem Rel_of : PO -> Rel.

   Definition SRel_of (p : PO) : Rel := fun x y : U => Rel_of p x y /\ x <> y.
   
End Partial_orders.
Hint Unfold Carrier_of Rel_of. *)
Require Import podefs_1.
(* podefs_1:
Require Import Ensembles.
Require Import Relations_1.
Require Import podefs.

Section Bounds.
   Variable U : Type.
   Variable D : PO U.
   
   Let C := Carrier_of U D.
   
   Let R := Rel_of U D.
   
   Inductive Totally_ordered (B : Ensemble U) : Prop :=
       Totally_ordered_definition :
         (Included U B C ->
          forall x y : U, Included U (Couple U x y) B -> R x y \/ R y x) ->
         Totally_ordered B.
   
   Inductive Upper_Bound (B : Ensemble U) (x : U) : Prop :=
       Upper_Bound_definition :
         In U C x -> (forall y : U, In U B y -> R y x) -> Upper_Bound B x.
   
   Inductive Lower_Bound (B : Ensemble U) (x : U) : Prop :=
       Lower_Bound_definition :
         In U C x -> (forall y : U, In U B y -> R x y) -> Lower_Bound B x.
   
   Inductive Lub (B : Ensemble U) (x : U) : Prop :=
       Lub_definition :
         Upper_Bound B x ->
         (forall y : U, Upper_Bound B y -> R x y) -> Lub B x.
   
   Inductive Glb (B : Ensemble U) (x : U) : Prop :=
       Glb_definition :
         Lower_Bound B x ->
         (forall y : U, Lower_Bound B y -> R y x) -> Glb B x.
   
   Inductive Bottom (bot : U) : Prop :=
       Bottom_definition :
         In U C bot -> (forall y : U, In U C y -> R bot y) -> Bottom bot.
   
   Definition Compatible (x y : U) : Prop :=
     exists z : U,
       In U C x -> In U C y -> In U C z /\ Upper_Bound (Couple U x y) z.
   
   Inductive Directed (X : Ensemble U) : Prop :=
       Definition_of_Directed :
         Included U X C ->
         Non_empty U X ->
         (forall x1 x2 : U,
          Included U (Couple U x1 x2) X ->
          exists x3 : U, In U X x3 /\ Upper_Bound (Couple U x1 x2) x3) ->
         Directed X.
   
   Inductive Complete : Prop :=
       Definition_of_Complete :
         (exists bot : U, Bottom bot) ->
         (forall X : Ensemble U, Directed X -> exists bsup : U, Lub X bsup) ->
         Complete.
   
   Definition Cpo : Prop := Complete.
   
   Definition Chain : Prop := Totally_ordered C.
   
   Inductive Conditionally_complete : Prop :=
       Definition_of_Conditionally_complete :
         (forall X : Ensemble U,
          Included U X C ->
          (exists maj : U, Upper_Bound X maj) -> exists bsup : U, Lub X bsup) ->
         Conditionally_complete.
   
End Bounds.
Hint Unfold Carrier_of.
Hint Unfold Rel_of.
Hint Resolve Totally_ordered_definition Upper_Bound_definition
  Lower_Bound_definition Lub_definition Glb_definition Bottom_definition
  Definition_of_Complete Definition_of_Complete
  Definition_of_Conditionally_complete. *)

Section The_power_set_partial_order.
   Variable U : Type.
   
   Inductive Power_set (A : Ensemble U) : Ensemble (Ensemble U) :=
       Definition_of_Power_set :
         forall X : Ensemble U,
         Included U X A -> In (Ensemble U) (Power_set A) X.
   Hint Resolve Definition_of_Power_set.
   
   Variable A : Ensemble U.
   
   Theorem Empty_set_minimal :
    forall X : Ensemble U, Included U (Empty_set U) X.
   Proof.
   red in |- *; intros X x H'; elim H'.
   Qed.
   Hint Resolve Empty_set_minimal.
   
   Theorem Power_set_non_empty :
    forall A : Ensemble U, Non_empty (Ensemble U) (Power_set A).
   Proof.
   intro A'; apply Non_empty_intro with (Empty_set U); auto.
   Qed.
   Hint Resolve Power_set_non_empty.
   
   Theorem Inclusion_is_an_order : Order (Ensemble U) (Included U).
   Proof.
   auto 8.
   Qed.
   Hint Resolve Inclusion_is_an_order.
   
   Theorem Inclusion_is_transitive : Transitive (Ensemble U) (Included U).
   elim Inclusion_is_an_order; auto.
   Qed.
   Hint Resolve Inclusion_is_transitive.
   
   Theorem Same_set_equivalence : Equivalence (Ensemble U) (Same_set U).
   Proof.
   generalize (Equiv_from_order (Ensemble U) (Included U)); intro H'; elim H';
    auto.
   Qed.

   Theorem Same_set_reflexive : Reflexive (Ensemble U) (Same_set U).
   Proof.
   elim Same_set_equivalence; auto.
   Qed.
   Hint Resolve Same_set_reflexive.
   
   Theorem Power_set_PO : PO (Ensemble U).
   Proof.
   apply Definition_of_PO with (Power_set A) (Included U); auto.
   Defined.
   
   Theorem Union_minimal :
    forall a b X : Ensemble U,
    Included U a X -> Included U b X -> Included U (Union U a b) X.
   Proof.
   intros a b X H' H'0; red in |- *; (intros x H'1; elim H'1); auto.
   Qed.
   Hint Resolve Union_minimal.
   
   Theorem Intersection_maximal :
    forall a b X : Ensemble U,
    Included U X a -> Included U X b -> Included U X (Intersection U a b).
   Proof.
   auto.
   Qed.
   
   Theorem Union_increases_l :
    forall a b : Ensemble U, Included U a (Union U a b).
   Proof.
   auto.
   Qed.
   
   Theorem Union_increases_r :
    forall a b : Ensemble U, Included U b (Union U a b).
   Proof.
   auto.
   Qed.
   
   Theorem Intersection_decreases_l :
    forall a b : Ensemble U, Included U (Intersection U a b) a.
   Proof.
   intros a b; red in |- *; auto.
   intros x H'; elim H'; auto.
   Qed.
   
   Theorem Intersection_decreases_r :
    forall a b : Ensemble U, Included U (Intersection U a b) b.
   Proof.
   intros a b; red in |- *; auto.
   intros x H'; elim H'; auto.
   Qed.
   Hint Resolve Union_increases_l Union_increases_r Intersection_decreases_l
     Intersection_decreases_r.
   
   Theorem Empty_set_is_Bottom :
    Bottom (Ensemble U) Power_set_PO (Empty_set U).
   Proof.
   apply Bottom_definition; simpl in |- *; auto.
   Qed.
   Hint Resolve Empty_set_is_Bottom.
  
   Theorem Union_is_Lub :
    forall a b : Ensemble U,
    Included U a A ->
    Included U b A ->
    Lub (Ensemble U) Power_set_PO (Couple (Ensemble U) a b) (Union U a b).
   Proof.
   intros a b H' H'0.
   apply Lub_definition; simpl in |- *.
   apply Upper_Bound_definition; simpl in |- *.
   auto.
   intros y H'1; elim H'1; auto.
   intros y H'1; elim H'1; simpl in |- *; auto.
   Qed.

   Theorem Intersection_is_Glb :
    forall a b : Ensemble U,
    Included U a A ->
    Included U b A ->
    Glb (Ensemble U) Power_set_PO (Couple (Ensemble U) a b)
      (Intersection U a b).
   Proof.
   intros a b H' H'0.
   apply Glb_definition.
   apply Lower_Bound_definition; simpl in |- *.
   apply Definition_of_Power_set; auto.
   generalize Inclusion_is_transitive; intro IT; red in IT; apply IT with a;
    auto.
   intros y H'1; elim H'1; auto.
   intros y H'1; elim H'1; simpl in |- *; auto.
   Qed.
   
   Theorem Empty_set_zero :
    forall X : Ensemble U, Union U (Empty_set U) X = X.
   Proof.
   auto 10.
   Qed.
   
   Theorem Union_commutative :
    forall A B : Ensemble U, Union U A B = Union U B A.
   Proof.
   auto.
   Qed.
   
   Theorem Union_associative :
    forall A B C : Ensemble U,
    Union U (Union U A B) C = Union U A (Union U B C).
   Proof.
   auto 20.
   Qed.
   
   Theorem Non_disjoint_union :
    forall (X : Ensemble U) (x : U),
    In U X x -> Union U (Singleton U x) X = X.
   Proof.
   intros X x H'; try assumption; auto 10.
   apply Extensionality_Ensembles; unfold Same_set in |- *; split;
    auto.
   red in |- *; auto 10.
   intros x0 H'0; elim H'0; auto 10.
   intros x1 H'1; elim H'1; auto 10.
   Qed.
   
   Theorem Finite_plus_one_is_finite :
    forall (X : Ensemble U) (x : U),
    Finite U X -> Finite U (Union U (Singleton U x) X).
   Proof.
   intros X x H'.
   generalize (classic (In U X x)); intro h; elim h;
    [ intro H'0; clear h; try exact H'0 | clear h; intro H'0 ];
    auto 10.
   generalize (Non_disjoint_union X x); intro h; lapply h;
    [ intro H'1; rewrite H'1; clear h | clear h ]; 
    auto.
   Qed.
   Hint Resolve Finite_plus_one_is_finite.
  
   Theorem Singleton_is_finite : forall x : U, Finite U (Singleton U x).
   Proof.
   intro x; generalize (Empty_set_zero (Singleton U x)); intro h;
    rewrite <- h; clear h.
   generalize (Union_commutative (Empty_set U) (Singleton U x)); intro h;
    rewrite h; clear h; auto.
   Qed.
   Hint Resolve Singleton_is_finite.
   
   Theorem Union_of_finite_is_finite :
    forall X Y : Ensemble U,
    Finite U X -> Finite U Y -> Finite U (Union U X Y).
   Proof.
   intros X Y H'; elim H'.
   generalize (Empty_set_zero Y); intro h; rewrite h; clear h; auto.
   clear A.
   intros AA H'0 H'1 x H'2 H'3.
   generalize (Union_associative (Singleton U x) AA Y); intro h; rewrite h;
    clear h; auto.
   Qed.
   
End The_power_set_partial_order.
Hint Resolve Empty_set_minimal.
Hint Resolve Power_set_non_empty.
Hint Resolve Inclusion_is_an_order.
Hint Resolve Inclusion_is_transitive.
Hint Resolve Same_set_reflexive.
Hint Resolve Union_minimal.
Hint Resolve Same_set_reflexive.
Hint Resolve Union_increases_l.
Hint Resolve Union_increases_r.
Hint Resolve Intersection_decreases_l.
Hint Resolve Intersection_decreases_r.
Hint Resolve Empty_set_is_Bottom.
Hint Resolve Empty_set_zero.
Hint Resolve Finite_plus_one_is_finite.
Hint Resolve Singleton_is_finite.

