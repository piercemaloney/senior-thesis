
Require Export Category.
(* Category:
Require Export Map2.

Set Implicit Arguments.
Unset Strict Implicit.

Section cat.

Variables (Ob : Type) (Hom : Ob -> Ob -> Setoid).

Infix "-->" := Hom (at level 95, right associativity).

Variable Op_comp : forall a b c : Ob, Map2 (a --> b) (b --> c) (a --> c).

Definition Cat_comp (a b c : Ob) (f : a --> b) (g : b --> c) :=
  Op_comp a b c f g.

Infix "o" := Cat_comp (at level 20, right associativity).

Definition Assoc_law :=
  forall (a b c d : Ob) (f : a --> b) (g : b --> c) (h : c --> d),
  f o g o h =_S (f o g) o h.

Variable Id : forall a : Ob, a --> a.

Definition Idl_law := forall (a b : Ob) (f : a --> b), Id _ o f =_S f.

Definition Idr_law := forall (a b : Ob) (f : b --> a), f =_S f o Id _.

End cat.

Structure Category : Type := 
  {Ob :> Type;
   Hom : Ob -> Ob -> Setoid;
   Op_comp : forall a b c : Ob, Map2 (Hom a b) (Hom b c) (Hom a c);
   Id : forall a : Ob, Hom a a;
   Prf_ass : Assoc_law Op_comp;
   Prf_idl : Idl_law Op_comp Id;
   Prf_idr : Idr_law Op_comp Id}.

Definition Comp (C : Category) := Cat_comp (Op_comp (c:=C)).

Infix "-->" := Hom (at level 95, right associativity).
Infix "o" := Comp (at level 20, right associativity).

Lemma Ass :
 forall (C : Category) (a b c d : C) (f : a --> b) 
   (g : b --> c) (h : c --> d), f o g o h =_S (f o g) o h.

Lemma Ass1 :
 forall (C : Category) (a b c d : C) (f : a --> b) 
   (g : b --> c) (h : c --> d), (f o g) o h =_S f o g o h.

Lemma Idl : forall (C : Category) (a b : C) (f : a --> b), Id _ o f =_S f.

Lemma Idl1 : forall (C : Category) (a b : C) (f : a --> b), f =_S Id _ o f.

Lemma Idr : forall (C : Category) (a b : C) (f : b --> a), f =_S f o Id _.

Lemma Idr1 : forall (C : Category) (a b : C) (f : a --> b), f o Id _ =_S f.

Lemma Idrl :
 forall (C : Category) (a b : C) (f : a --> b), f o Id _ =_S Id _ o f.

Lemma Idlr :
 forall (C : Category) (a b : C) (f : a --> b), Id _ o f =_S f o Id _.

Section composition_to_operator.

Variables (A : Type) (H : A -> A -> Setoid)
  (Cfun : forall a b c : A, H a b -> H b c -> H a c).

Definition Congl_law :=
  forall (a b c : A) (f g : H b c) (h : H a b),
  f =_S g -> Cfun h f =_S Cfun h g. 

Definition Congr_law :=
  forall (a b c : A) (f g : H a b) (h : H b c),
  f =_S g -> Cfun f h =_S Cfun g h. 

Definition Cong_law :=
  forall (a b c : A) (f f' : H a b) (g g' : H b c),
  f =_S f' -> g =_S g' -> Cfun f g =_S Cfun f' g'. 

Hypothesis pcgl : Congl_law.
Hypothesis pcgr : Congr_law.

Variable a b c : A.

Definition Build_Comp :=
  Build_Map2 (pcgl (a:=a) (b:=b) (c:=c)) (pcgr (a:=a) (b:=b) (c:=c)).

End composition_to_operator.

Section cat_cong.

Variable C : Category.

Lemma Comp_l :
 forall (a b c : C) (f g : b --> c) (h : a --> b), f =_S g -> h o f =_S h o g. 

Lemma Comp_r :
 forall (a b c : C) (f g : a --> b) (h : b --> c), f =_S g -> f o h =_S g o h. 

Lemma Comp_lr :
 forall (a b c : C) (f f' : a --> b) (g g' : b --> c),
 f =_S f' -> g =_S g' -> f o g =_S f' o g'. 

End cat_cong. *)

Set Implicit Arguments.
Unset Strict Implicit.

Section discrete.

Variable U : Type.

Inductive Discr_mor : U -> U -> Type :=
    Build_Discr_mor : forall a : U, Discr_mor a a.

 Section disc_mor_setoid.

 Variable a b : U.

 Definition Equal_Discr_mor (f g : Discr_mor a b) := True.

 Lemma Equal_Discr_mor_equiv : Equivalence Equal_Discr_mor.
 Proof.
 unfold Equal_Discr_mor in |- *.
 apply Build_Equivalence; [ trivial | apply Build_Partial_equivalence ];
  red in |- *; trivial.
 Qed.

 Canonical Structure Discr_mor_setoid := Build_Setoid Equal_Discr_mor_equiv.

 End disc_mor_setoid.

Definition Comp_Discr_mor (a b c : U) (f : Discr_mor a b) :=
  match f in (Discr_mor d d0) return (Discr_mor d0 c -> Discr_mor d c) with
  | Build_Discr_mor x =>
      fun g : Discr_mor x c =>
      match g in (Discr_mor d d0) return (Discr_mor d d0) with
      | Build_Discr_mor x => Build_Discr_mor x
      end
  end.
   
Lemma Comp_Discr_congl : Congl_law Comp_Discr_mor.
Proof.
unfold Congl_law in |- *; simpl in |- *; unfold Equal_Discr_mor in |- *.
intros a b c f g h H; elim H; trivial.
Qed.

Lemma Comp_Discr_congr : Congr_law Comp_Discr_mor.
Proof.
unfold Congr_law, Comp_Discr_mor in |- *; simpl in |- *;
 unfold Equal_Discr_mor in |- *; trivial.
Qed.

Definition Comp_Discr := Build_Comp Comp_Discr_congl Comp_Discr_congr.

Lemma Assoc_Discr : Assoc_law Comp_Discr.
Proof.
unfold Assoc_law in |- *; simpl in |- *.
unfold Comp_Discr, Equal_Discr_mor in |- *; trivial.
Qed.

Definition Id_Discr (a : U) := Build_Discr_mor a.

Lemma Idl_Discr : Idl_law Comp_Discr Id_Discr.
Proof.
unfold Idl_law in |- *; simpl in |- *.
unfold Comp_Discr, Id_Discr, Equal_Discr_mor in |- *; trivial.
Qed.

Lemma Idr_Discr : Idr_law Comp_Discr Id_Discr.
Proof.
unfold Idr_law in |- *; simpl in |- *.
unfold Comp_Discr, Id_Discr, Equal_Discr_mor in |- *; trivial.
Qed.

Canonical Structure Discr := Build_Category Assoc_Discr Idl_Discr Idr_Discr.

End discrete.

Axiom
  Discr_mor_ind1 :
    forall (U : Type) (a : U) (P : Discr_mor a a -> Prop),
    P (Build_Discr_mor a) -> forall x : Discr_mor a a, P x.

