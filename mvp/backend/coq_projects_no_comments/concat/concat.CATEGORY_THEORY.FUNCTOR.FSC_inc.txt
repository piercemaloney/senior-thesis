
Require Export FullSubCat.
Require Export FunctorProperty.

Set Implicit Arguments.
Unset Strict Implicit.

Section fsc_inc_def.

Variables (C : Category) (I : Type) (a : I -> C).

Definition Inc_map (i j : FullSubCat a) := Id_map (a i --> a j).

Lemma Inc_comp_law : Fcomp_law Inc_map.
Proof.
unfold Fcomp_law in |- *; simpl in |- *.
intros i1 i2 i3 f g; apply Refl.
Qed.

Lemma Inc_id_law : Fid_law Inc_map.
Proof.
unfold Fid_law in |- *; simpl in |- *.
intros i; apply Refl.
Qed.

Canonical Structure FSC_inc := Build_Functor Inc_comp_law Inc_id_law.

Lemma Inc_faith : Faithful_law FSC_inc.
Proof.
unfold Faithful_law in |- *; simpl in |- *.
unfold FMor in |- *; simpl in |- *; auto.
Qed.

Lemma Inc_full :
 Full_law (F:=FSC_inc) (fun i j : I => Id_fun (A:=a i --> a j)).
Proof.
unfold Full_law in |- *; simpl in |- *; intros i j f.
apply Refl.
Qed.

End fsc_inc_def.
