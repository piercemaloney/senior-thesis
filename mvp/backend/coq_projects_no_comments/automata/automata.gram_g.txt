
Require Import Ensf.
Require Import Words.
Require Import fonctions.
Require Import Relations.
Require Import gram.

Inductive Deriveg (X R : Ensf) : Word -> Word -> Prop :=
  | Deriveg1 :
      forall (u v : Word) (A : Elt),
      dans (couple A (word u)) R -> Deriveg X R (cons A v) (Append u v)
  | Deriveg2 :
      forall (u v : Word) (x : Elt),
      dans x X -> Deriveg X R u v -> Deriveg X R (cons x u) (cons x v).

Hint Resolve Deriveg1.
Hint Resolve Deriveg2.

Definition Derivegstar (X R : Ensf) := Rstar Word (Deriveg X R).

Lemma Deriveg_Derive :
 forall (X R : Ensf) (u v : Word), Deriveg X R u v -> Derive R u v.
intros X R u v Der_g.
elim Der_g.
intros.
apply Derive1; auto.
intros.
apply Derive2; auto.
Qed.

Lemma Derivegstar_Derivestar :
 forall (X R : Ensf) (u v : Word), Derivegstar X R u v -> Derivestar R u v.
unfold Derivegstar, Rstar, Derivestar in |- *.
intros X R x y Derivegstar_x_y.
pattern x, y in |- *.
apply Derivegstar_x_y.
intro. apply Rstar_reflexive.
intros u v w Der Der_star.
apply Rstar_R with v.
apply Deriveg_Derive with X; assumption.
assumption.
Qed.

Axiom
  Derivestar_Derivegstar :
    forall (X R : Ensf) (u v : Word), Derivestar R u v -> Derivegstar X R u v.

Hint Resolve Derivestar_Derivegstar.